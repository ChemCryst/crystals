%SCRIPT XCIF
%% The variables needed by XQUESTIO
%    VARIABLE CHARACTER CFILE
%    VARIABLE LOGICAL JUSTRUNIT TWINNED HINCELL VALID11
%    VARIABLE INTEGER FTYPE ISQUEEZE
%    EVALUATE ISQUEEZE = - 1
%    VARIABLE REAL SOESD
%    VARIABLE INTEGER INVERTTYPE ICENTRIC
%    VARIABLE INTEGER F67
%%
%%   Remember that XCIF includes a call to 
%%   TO #CIF, and hence XSFLB and #esd to ensure that values are up to date
%%
%    copy '#set moni fast'
%%
%%
%    COPY '#GENERALEDIT 11'
%    COPY 'LOCATE RECORDTYPE=103'
%    COPY 'TRANSFER FROM OFFSET=30 TO INVERTTYPE'
%    COPY 'END'
%    IF ( INVERTTYPE .LT. 0 ) THEN
{E Cannot ouput cif file with esds. Inverted matrix cannot be trusted
%%      copy '#set moni on'
%      FINISH
%    END IF
%%
%    EVALUATE TWINNED = FALSE
%    EVALUATE F67 = 6
%    BLOCK
%      VARIABLE INTEGER ITMP
%      IF ( EXISTS 13 .GT. 0 ) THEN
%        COPY '#GENERALEDIT 13'
%        COPY 'LOCATE RECORDTYPE=101'
%        COPY 'TRANSFER FROM OFFSET=1 TO ITMP'
%        IF ( ITMP .GE. 0 ) THEN
%         EVALUATE TWINNED = TRUE
%         EVALUATE F67 = 7
%        END IF
%        COPY 'END'
%      END IF
%    END BLOCK
%% Find out if  H is in list 29. This sets HINCELL to true or false.
%    COPY '#SCRIPT CHECK29'
%%
%      IF ( EXISTS 2 .GT. 0 ) THEN
%          COPY '#GENERALEDIT 2'
%          COPY 'LOCATE RECORDTYPE=101'
%          COPY 'TRANSFER FROM OFFSET =  0 TO ICENTRIC'
%          COPY 'END'
%      END IF
%%
%% Check shift/esd
%      IF ( EXISTS 30 .GT. 0 ) THEN
%          COPY '#GENERALEDIT 30'
%          COPY 'LOCATE RECORDTYPE=103'
%          COPY 'TRANSFER FROM OFFSET =  7 TO SOESD'
%          COPY 'END'
%      END IF
%%
%% Find refinement type
%      IF ( EXISTS 23 .GT. 0 ) THEN
%          COPY '#GENERALEDIT 23'
%          COPY 'LOCATE RECORDTYPE=101'
%          COPY 'TRANSFER FROM OFFSET =  4 TO ISQUEEZE'
%          COPY 'LOCATE RECORDTYPE=103'
%          COPY 'TRANSFER FROM OFFSET =  1 TO FTYPE'  %% F or Fsq
%          COPY 'END'
%      END IF
%%
%% Check list 11 is up to date
%    COPY '#SCRIPT VALID11'
%%
%%
^^WI WINDOW XCIF 'Output CIF' MODAL COMMIT='CIFOK' CANCEL='CIFXX'
^^WI GRID   GRIDM NROWS=12 NCOLS=3
^^WI {
^^WI  @ 2,2 BUTTON GOODY 'Edit CIF Goodies...'
^^WI  @ 4,2 GRID GRIDA NROWS=2 NCOLS=3 OUTLINE='Recalculate'
^^WI  {
^^WI    @ 1,1 CHECKBOX XREF 'Six final cycles'
%    IF ( SOESD .GT. 0.01 ) .OR. ( .NOT. VALID11 ) THEN
^^WI      STATE=ON
%    ELSE
^^WI      STATE=OFF
%    END IF
%    IF ( .NOT. VALID11 ) THEN
^^WI      DISABLED=YES
%    END IF
^^WI    @ 1,3 CHECKBOX XFOU 'Difference Fourier map' STATE=ON
%    IF ( .NOT. VALID11 ) THEN
^^WI      DISABLED=YES
%    END IF
^^WI    @ 2,3 CHECKBOX XABS 'Check Absolute Structure' 
%        IF ICENTRIC .EQ. 0 THEN 
^^WI        STATE = ON
%        ELSE
^^WI        STATE = OFF
%        END IF
^^WI  }
^^WI  @ 5,2 GRID GRIDA NROWS=1 NCOLS=2 OUTLINE='H-bonds'
^^WI  {
^^WI    @ 1,1 CHECKBOX XHBD 'List H-bonds'
% IF ( HINCELL ) THEN
^^WI     STATE=ON
% ELSE
^^WI     DISABLED=YES STATE=OFF
% END IF
^^WI  }
^^WI  @ 6,2 GRID GRIDV NROWS=3 NCOLS=3 OUTLINE='Display'
^^WI  {
^^WI    @ 1,1 RADIOBUTTON CFNONE 'None' STATE=ON
^^WI    @ 2,1 RADIOBUTTON CFNOTE 'Default editor' 
^^WI    @ 3,1 RADIOBUTTON CFCCDC 'EnCIFer' 
^^WI    @ 1,3 RADIOBUTTON CFPLAT 'Platon' 
^^WI    @ 2,3 RADIOBUTTON CFCHKC 'Platon checkcif' 
^^WI    @ 3,3 RADIOBUTTON CFCCDM 'Mercury' 
^^WI  }
^^WI  @ 7,2 GRID GRIDAC NROWS=4 NCOLS=1 OUTLINE='Additional FCF file'
^^WI  {
^^WI    @ 1,1 RADIOBUTTON FCFD 'Reflections as in refinement' STATE=ON
^^WI    @ 2,1 RADIOBUTTON FCFN 'No output'
^^WI    @ 3,1 RADIOBUTTON FCFS 'Reflections with F-squared values'
^^WI    @ 4,1 RADIOBUTTON FCFF 'Reflections with F values'
^^WI  }
^^WI  @ 9,2 GRID GRIDHK NROWS=1 NCOLS=2 OUTLINE='Embed other files'
^^WI  {
^^WI    @ 1,1 CHECKBOX XHKL 'Embed HKLI data'   STATE=ON
%     IF .NOT. FILEEXISTS ( 'ARCHIVE-HKL.CIF' ) THEN
^^WI    STATE=OFF  DISABLED=YES
%     END IF
^^WI    @ 1,2 CHECKBOX XSQZ 'Embed SQUEEZE data'   STATE=ON
%    IF ( ISQUEEZE .NE. 0 ) .OR.  -
        ( .NOT. FILEEXISTS ( 'platon_sqd.sqf' ) ) THEN
^^WI    STATE=OFF  DISABLED=YES
%    END IF
^^WI  }
^^WI  @ 11,2 GRID GRIDB NROWS=1 NCOLS=5
^^WI  {
^^WI    @ 1,1 BUTTON CIFOK '&Ok' DEFAULT
^^WI    @ 1,3 BUTTON CIFXX '&Cancel'
^^WI    @ 1,5 BUTTON CIFHELP    '&Help'
^^WI  }
^^WI }
^^WI SHOW
^^CR
%    LOOP
%     VERIFY CIFOK CIFXX CIFHELP GOODY 
%     GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%     CASE VALUE
%      BLOCK   % CIFOK
^^WI WINDOW XBLOCK 'CIF' MODAL GRID MAIN NROWS=3 NCOLS=3
^^WI { @ 2,2 STATIC T1 'CIF generation in progress' } SHOW
^^CR
%       EVALUATE LOOPEND = LOOPCOUNTER
%      END BLOCK
%      BLOCK   % CIFXX
^^CO    DISPOSE XCIF
%%       copy '#set moni on'
%       FINISH
%      END BLOCK
%      BLOCK   % CIFHELP
%      COPY '#SCRIPT XHELP1'
 CIF files
 ---------

 CIF is an International Union of Crystallography standard
 for archiving, exchanging and publishing crystallographic
 data. For details see Hall, S. R., Allen, F. H., &
 Brown, I. D. (1991). Acta Cryst A47, 655-685.

 When you click OK, a CIF file named 'publish.cif' will be
 created in the current folder.

 Options
 -------

 Edit CIF Goodies - opens a dialog for editing some of the
 stored experimental information before it is saved in
 the CIF file.
 
 Six final cycles - this confirms that the parameters are
 in a stable minimum if the shifts in all refined parameters
 are small.
 
 Fourier difference map - this recomputes a Fourier difference
 map so that the minimum and maximum electron density in
 the CIF are accurate.
 
 Display - includes options for opening the CIF in various
 programs after it is produced.
 
 Additional FCF file - Saves an additional file called publish.fcf
 containing the hkl data in either F or F-squared format.

 If either/both files ARCHIVE-HKL.CIF and platon_sqd.sqf exist, you
 will be able to embed them in the cif.


%       COPY '#SCRIPT XHELP2'

%      END BLOCK
%      BLOCK   % GOODY
%       COPY '#SCRIPT ED30'
%      END BLOCK
%     END CASE
%    END LOOP
%%
 * Producing a CIF listing of all data
%%
^^?? XREF STATE
%    VERIFY ON OFF
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    IF ( VALUE .EQ. 1 ) THEN
%      COPY '#SCRIPT SIXCYCLE'
%    END IF
%%
^^?? XABS STATE
%    VERIFY ON OFF
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    IF ( VALUE .EQ. 1 ) THEN
%%      IF ICENTRIC .EQ. 0 THEN           % NOTE: commented out
%%       COPY '#SCRIPT TONSWORK'          % NOTE: commented out
%%       COPY '#TON PLOT=NO PUNCH =NO'    % NOTE: commented out                                                     
%%       COPY 'END'                       % NOTE: commented out
%%      END IF                            % NOTE: commented out
%       COPY '#SCRIPT XABSOLUTE'
%    END IF
%%
%%
^^?? XFOU STATE
%    VERIFY ON OFF
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    IF ( VALUE .EQ. 1 ) THEN
%  
%% A final difference map.
%      COPY '#SCRIPT PREFOUR'
%      COPY 'MAP TYPE=DIFF'
%      COPY 'PEAK HEIGHT=1 '
%      COPY 'END'
%      COPY '#SCRIPT POSTFOUR'
{I Difference Fourier complete.
%    END IF
%%
%    copy '#sfls'  %Internal call to SFLS from XCIF removed Nov 2019 because
%    copy 'calc'   %LIST 33 common block was not correclty loaded, causing 
%    copy 'end'    %comotation errors if previous sfls had been on a de-twinned LIST 7
%    COPY "#BONDCALC FORCE"
%    COPY "END"
%    COPY "#STORE CSYS CIF 'publish.cif'"
%    COPY '#APPEND PUNCH '
%    COPY '#CIF '
%    COPY 'END'
%    COPY '#PARAMETERS'
%    COPY 'LAYOUT INSET = 1 NCHAR = 120 ESD=EXCLRH'
%    COPY 'COORD SELECT=ALL MONITOR=OFF PRINT=NO PUNCH=CIF NCHAR=14'
%    COPY "U'S MONITOR=OFF, PRINT=NO, PUNCH=NO, NCHAR=14"
%    COPY 'END'
%    COPY '#DIST'
%    COPY 'E.S.D YES YES'
%    COPY 'SELECT RANGE=L41'
%    COPY 'OUTPUT MON=OFF PUNCH = CIF HESD=NONFIXED'
%    COPY 'END'
%%
^^?? XHBD STATE
%    VERIFY ON OFF
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    IF ( VALUE .EQ. 1 ) THEN
%      COPY '#DISTANCE '
%      COPY 'OUT PUNCh=H-CIF mon=ang HESD=NONFIXED'
%      COPY 'SELECT RANGE=LIMIT'
%      COPY 'LIMIT 0.7 2.6 0.7 2.6 '
%      COPY 'E.S.D YES YES'
%      COPY 'PIVOT H '
%      COPY 'BOND O N C'
%      COPY 'END '
%    END IF
%%
%%  Write LIST 12 and 16 to end of cif
%      COPY '#PUNCH 12 C'
%      COPY 'END'
%      COPY '#PUNCH 16 C'
%      COPY 'END'
%%
%    copy '#set moni on'
%%   Write manually omitted reflections to end of cif
%      COPY '#SUM LIST 28 HI'
%      COPY 'END'
%%
^^?? XHKL STATE
%    VERIFY ON OFF
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    IF ( VALUE .EQ. 1 ) THEN
%     IF FILEEXISTS ( 'ARCHIVE-HKL.CIF' ) .EQ. TRUE THEN
%      COPY '#ADDARC ARCHIVE-HKL.CIF'
%      COPY 'END'
%     ELSE
{I Reflection archive ARCHIVE-HKL.CIF no longer exists
%     END IF
%    END IF
^^?? XSQZ STATE
%    VERIFY ON OFF
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    IF ( VALUE .EQ. 1 ) THEN
%      IF FILEEXISTS ( 'platon_sqd.sqf' ) .EQ. TRUE THEN
%       COPY '#ADDARC platon_sqd.sqf'
%       COPY 'END'
%      ELSE
{I Reflection archive platon_sqd.sqf no longer exists
%      END IF
%    END IF
%%
{I  Parameters saved in {&publish.cif{&
%%
%%
%%
^^?? FCFN STATE
%    VERIFY ON OFF
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    IF ( VALUE .EQ. 2 ) THEN
%      COPY '#OPEN FRN1 publish.fcf'
%%
%%     Ensure the refined values are output
%      EVALUATE F67 = 6
%%
^^?? FCFD STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%      IF ( VALUE .EQ. 2 ) THEN                         %% chosen by user                      
^^??   FCFS STATE
%        VERIFY ON OFF
%        GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%        IF ( VALUE .EQ. 1 ) THEN
%         TRANSFER '#PUNCH ' // CHARACTER ( F67 ) // ' H' TO CRYSTALS
%        ELSE
%         TRANSFER '#PUNCH ' // CHARACTER ( F67 ) // ' B' TO CRYSTALS
%        END IF
%        COPY 'END'
%      ELSE
%%                                                     As used in refinement
%        IF FTYPE .EQ. 0 THEN
%         TRANSFER '#PUNCH ' // CHARACTER ( F67 ) // ' H' TO CRYSTALS
%        ELSE
%         TRANSFER '#PUNCH ' // CHARACTER ( F67 ) // ' B' TO CRYSTALS
%        END IF
%        COPY 'END'
%      END IF
%      COPY '#RELEASE FRN1 logs/bfile.frn'
{I  Reflections saved in {&publish.fcf{&
%    END IF
%%
^^?? CFNOTE STATE
%    VERIFY ON OFF
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    IF ( VALUE .EQ. 1 ) THEN
%  VARIABLE CHARACTER READER_FILE
%  EVALUATE READER_FILE = 'publish.cif'
%  COPY '#SCRIPT XREADER'
%    ELSE
^^??   CFCCDC STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%      IF ( VALUE .EQ. 1 ) THEN
{I Starting enCIFer to edit publish.cif
%        COPY '#SCRIPT encifer'
%      ELSE
^^??     CFPLAT STATE
%        VERIFY ON OFF
%        GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%        IF ( VALUE .EQ. 1 ) THEN
{I Starting Platon with publish.cif
%          COPY '#SCRIPT platona'
%        ELSE
^^??       CFCHKC STATE
%          VERIFY ON OFF
%          GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%          IF ( VALUE .EQ. 1 ) THEN
{I Starting Platon to check publish.cif
%            COPY '#SCRIPT platonc'
%          ELSE
^^??         CFCCDM STATE
%            VERIFY ON OFF
%            GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%            IF ( VALUE .EQ. 1 ) THEN
%              EVALUATE JUSTRUNIT = TRUE
{I Starting Mercury with publish.cif
%              COPY '#SCRIPT mercury'
%            END IF
%          END IF
%        END IF
%      END IF
%    END IF
^^CO DISPOSE XBLOCK DISPOSE XCIF
%%
%    copy '#set moni on'
%END SCRIPT
