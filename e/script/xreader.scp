%SCRIPT XREADER
%%
% VARIABLE CHARACTER READER_FILE
% VARIABLE LOGICAL READER_OK
% EVALUATE READER_OK = FALSE
%% 
%% Check it exists
%%
% IF ( .NOT. ( FILEEXISTS ( READER_FILE ) ) ) THEN

 The file passed to xeditor for editing does not exist.
%    SHOW READER_FILE
%    FINISH
% END IF 
%%%%
%%
%% Check for alternative editor user setting
%%
^^CO GETKEY CRYEDITOR
%  GET SILENT NOSTORE FINAL TEXT '1' '1'                                        
%  IF CVALUE .NE. '1' .AND. FILEEXISTS ( CVALUE ) THEN 
Using user-specified editor.
%%    TRANSFER "#SPAWN +'" // CVALUE // "' " // READER_FILE TO DISPLAY
%    TRANSFER '#SPAWN +"' // CVALUE // '" ' // READER_FILE TO CRYSTALS
%    EVALUATE READER_OK = TRUE
%    FINISH
%  END IF
%%
%%
%TRANSFER '^^WI WINDOW XREADER "Viewing ' // READER_FILE // '"' TO DISPLAY
^^WI SIZE MODAL KEEP
^^WI DEFINEMENU MAIN 'NULL'
^^WI  MENU _FILE    '&File'
^^WI   ITEM CLOSE   '&Close	ALT+F4'  'BOK'
^^WI  ENDMENU
^^WI  MENU _SEARCH    '&Search'
^^WI   ITEM FIND    '&Find	Ctrl+F' 'FIND' 
^^WI   ITEM FIND    '&Find next	F3' 'NEXT' 
^^WI  ENDMENU
^^WI ENDDEFINEMENU
^^WI GRID OUTER NROWS=3 NCOLS=5
^^WI {
^^WI   @ 2,2 MULTIEDIT TEXTR '' NROWS=25 NCOLS=80
^^WI        IGNORE DISABLED=YES
^^WI   @ 2,4 GRID BTNS NROWS=1 NCOLS=1
^^WI   {
^^WI     @ 1,1 BUTTON BOK '&Close' DEFAULT COMMIT CANCEL
^^WI   }
^^WI }
^^WI SHOW
^^CR
%TRANSFER '^^CO SET TEXTR LOAD "' // READER_FILE // '"' TO DISPLAY
%%
% LOOP
%   VERIFY BOK FIND NEXT
%   GET SILENT NOSTORE FINAL ABBREVIATED 'BOK?' ' '
%   IF VALUE .EQ. 1 THEN
^^CO   DISPOSE XREADER
%      EVALUATE READER_OK = TRUE
%      FINISH
%   ELSE IF ( VALUE .EQ. 2 ) THEN
%% Search
%TRANSFER "^^CO SET TEXTR FIND" TO DISPLAY
%   ELSE IF ( VALUE .EQ. 3 ) THEN
%% Search
%TRANSFER "^^CO SET TEXTR NEXT" TO DISPLAY
%   END IF
% END LOOP
%END SCRIPT
