%SCRIPT MASAATOM
%%
%% Launch search for fragment based on a single selected peak. 
%% Store coords, remove peak / atom. Remove any other peaks.
%%
% VARIABLE LOGICAL   CALLED LT
% VARIABLE CHARACTER CATOM MCFILE 
% VARIABLE REAL      MUISO MOCC MJITTER
% VARIABLE INTEGER   MCYCLES
%%
%% Get details from GUI:
%%
% IF CALLED .EQ. FALSE THEN
%    GET SILENT NOSTORE TEXT 'Atom?' ' '
%    EVALUATE CATOM = CVALUE
% END IF

% TRANSFER 'Atom ' // CATOM TO DISPLAY

^^WI WINDOW MASADIALOG 'Fragment search' MODAL COMMIT='BUTTONOK' CANCEL='BUTTONXX'         
^^WI GRID GRIDM NROWS=1 NCOLS=2                                             
^^WI {
^^WI  @ 1,2 GRID GRIDB NROWS=7 NCOLS=3
^^WI  { 
^^WI    @ 2,2 BUTTON BUTTONOK '&Ok' DEFAULT
^^WI    @ 4,2 BUTTON BUTTONXX '&Cancel' 
^^WI    @ 6,2 BUTTON BHELP    '&Help'
^^WI  }
^^WI  @ 1,1 GRID GRIDAB NROWS=5 NCOLS=3 
^^WI  {                             
^^WI    @ 1,2 GRID GRIDABA NROWS=1 NCOLS=1  
^^WI    {
^^WI     @ 1,1 LISTBOX MFRG VISLINES=6
^^WI           ADDTOLIST 'ethanol' 'methanol' 'PF6-'
^^WI                     'BF4-' 'toluene' 'benzene'
^^WI                     'isopropanol' 'acetone' 'DMF' 
^^WI                     'l-camphor' NULL
^^WI    }
^^WI    @ 3,2 GRID GRIDABB NROWS=7 NCOLS=5
^^WI    {
^^WI     @ 1,1 STATIC TEXT2 'Cycles'                                 
^^WI     @ 3,1 STATIC TEXT2 'Occupancy'                                 
^^WI     @ 5,1 STATIC TEXT2 'U[iso]'                                 
^^WI     @ 7,1 STATIC TEXT2 'Jitter'
^^WI     @ 5,5 STATIC TEXT2 '\AA\**2'  
^^WI     @ 7,5 STATIC TEXT2 '\AA'
^^WI     @ 1,3 EDITBOX MCYC '100' CHARS=9 INTEGER
^^WI     @ 3,3 EDITBOX MOCC '1.0' CHARS=9 REAL 
^^WI     @ 5,3 EDITBOX MISO '0.05' CHARS=9 REAL 
^^WI     @ 7,3 EDITBOX MJIT '1.5' CHARS=9 REAL 
^^WI    }
^^WI    @ 5,2 GRID GRIDABC NROWS=3 NCOLS=1
^^WI    {
^^WI      @ 1,1 CHECKBOX MDEL 
% TRANSFER "^^WI 'Delete " // CATOM // " before run'" TO DISPLAY
% TRANSFER " 'Delete " // CATOM // " before run'" TO DISPLAY
^^WI        INFORM=NO STATE=ON
^^WI      @ 3,1 CHECKBOX MRESET 'Delete best fit data from previous runs'
^^WI        INFORM=NO
%  IF ( FILEEXISTS ( 'bestfrag.dat' ) ) THEN
^^WI        STATE=ON
%  ELSE
^^WI        STATE=OFF DISABLED=YES
%  END IF
^^WI    }
^^WI  } 
^^WI }  
^^WI SHOW 
^^CR    

Waiting

% VERIFY BUTTONOK BUTTONXX BHELP 
% GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                                  
% CASE VALUE                                                                    
%   BLOCK           %BUTTONOK%                                                  
%% delete clicked atom?
%       COPY '#EDIT 5 10'
%       CLEAR
%       INSERT 'KEEP '
%       STORE CHARACTER CATOM
%       SEND
%       COPY 'end'
^^??    MDEL STATE                                                            
%       VERIFY ON OFF                                                           
%       GET SILENT NOSTORE FINAL ABBREVIATED ' '
%       IF VALUE .EQ. 1 THEN
%         COPY '#EDIT'
%         TRANSFER 'delete ' // CATOM TO CRYSTALS
%         COPY 'end'
%       END IF
^^??    MRESET STATE                                                            
%       VERIFY ON OFF                                                           
%       GET SILENT NOSTORE FINAL ABBREVIATED ' '
%       IF VALUE .EQ. 1 THEN
%         EVALUATE LT = FILEDELETE ( 'bestfrag.dat' )
%       END IF
^^??    MCYC TEXT
%       GET SILENT NOSTORE INTEGER ' '                                             
%       EVALUATE MCYCLES = VALUE
^^??    MOCC TEXT
%       GET SILENT NOSTORE REAL ' '                                             
%       EVALUATE MOCC = VALUE
^^??    MISO TEXT
%       GET SILENT NOSTORE REAL ' '                                             
%       EVALUATE MUISO = VALUE
^^??    MJIT TEXT
%       GET SILENT NOSTORE REAL ' '                                             
%       EVALUATE MJITTER = VALUE
^^??    MFRG SELECTED  
%       GET SILENT FINAL NOSTORE INTEGER ' '                                
%       CASE VALUE
%         EVALUATE MCFILE = 'ethanol.frg'                                          
%         EVALUATE MCFILE = 'methanol.frg'                                          
%         EVALUATE MCFILE = 'CRYSDIR:script/pf6.frg'                                          
%         EVALUATE MCFILE = 'CRYSDIR:script/bf4.frg'                                          
%         EVALUATE MCFILE = 'toluene.frg'                                          
%         EVALUATE MCFILE = 'benzene.frg'                                          
%         EVALUATE MCFILE = 'isopropanol.frg'                                          
%         EVALUATE MCFILE = 'acetone.frg'                                          
%         EVALUATE MCFILE = 'dmf.frg'                                          
%         EVALUATE MCFILE = 'CRYSDIR:script/lcamphor.frg'                                          
%       END CASE
^^CO    DISPOSE MASADIALOG
%       COPY '#SCRIPT MASAVOID3'                                                      
%   END BLOCK
%   BLOCK                   %BUTTONXX%                                          
^^WI    DISPOSE MASADIALOG
^^CR                                                                            
%       FINISH                                                                  
%   END BLOCK                                                                   
%   BLOCK                   %BHELP%                                             
%       COPY '#SCRIPT XHELP1'                                                   
%       COPY '#SCRIPT XHELP2'                                                   
%   END BLOCK                                                                   
% END CASE                                                                      
%END SCRIPT                                                                     
