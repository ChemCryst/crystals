%SCRIPT MASAATOM
%%
%% Launch search for fragment based on a single selected peak. 
%% Store coords, remove peak / atom. Remove any other peaks.
%%
% VARIABLE LOGICAL   CALLED LT
% VARIABLE CHARACTER CATOM MCFILE 
% VARIABLE REAL      MUISO MOCC MJITTER
% VARIABLE INTEGER   MCYCLES MNFRAG
%%
%% Get details from GUI:
%%
% IF CALLED .EQ. FALSE THEN
%    GET SILENT NOSTORE TEXT 'Atom?' ' '
%    EVALUATE CATOM = CVALUE
% END IF
%%
% TRANSFER 'Atom ' // CATOM TO DISPLAY
%%
^^WI WINDOW MASADIALOG 'Fragment search' MODAL COMMIT='BUTTONOK' CANCEL='BUTTONXX'         
^^WI GRID GRIDM NROWS=1 NCOLS=2                                             
^^WI {
^^WI  @ 1,2 GRID GRIDB NROWS=7 NCOLS=3
^^WI  { 
^^WI    @ 2,2 BUTTON BUTTONOK '&Ok' DEFAULT
^^WI    @ 4,2 BUTTON BUTTONXX '&Cancel' 
^^WI    @ 6,2 BUTTON BHELP    '&Help'
^^WI  }
^^WI  @ 1,1 GRID GRIDAB NROWS=9 NCOLS=3 
^^WI  {                             
^^WI    @ 2,2 GRID GRIDABA NROWS=1 NCOLS=1  
^^WI    {
^^WI     @ 1,1 LISTBOX MFRG VISLINES=6
^^WI           ADDTOLIST 'PF6-'  'BF4-' 
^^WI  'methanol' 'methanol (rigid)' 'DCM' 'ethanol' 
^^WI  'toluene' 'benzene'
^^WI  'isopropanol' 'acetone' 'DMF' 
^^WI  'l-camphor' NULL
^^WI    }
^^WI    @ 4,2 GRID GRIDABB NROWS=7 NCOLS=5
^^WI    {
^^WI     @ 1,1 STATIC TEXT2 'Cycles'                                 
^^WI     @ 3,1 STATIC TEXT2 'Occupancy'                                 
^^WI     @ 5,1 STATIC TEXT2 'U[iso]'                                 
^^WI     @ 7,1 STATIC TEXT2 'Jitter'
^^WI     @ 5,5 STATIC TEXT2 '\AA\**2'  
^^WI     @ 7,5 STATIC TEXT2 '\AA'
^^WI     @ 1,3 EDITBOX MCYC '100' CHARS=9 INTEGER
^^WI     @ 3,3 EDITBOX MOCC '1.0' CHARS=9 REAL 
^^WI     @ 5,3 EDITBOX MISO '0.05' CHARS=9 REAL 
^^WI     @ 7,3 EDITBOX MJIT '1.5' CHARS=9 REAL 
^^WI    }
^^WI    @ 6,2 GRID GRIDABC NROWS=1 NCOLS=5
^^WI    {
^^WI      @ 1,1 CHECKBOX MPO 'Fit ' INFORM=YES STATE=OFF
^^WI      @ 1,3 EDITBOX  MNFRAG '2' CHARS=4 INTEGER DISABLED=YES
^^WI      @ 1,5 STATIC T1 ' overlapping fragments'
^^WI    } 
^^WI    @ 8,2 GRID GRIDABD NROWS=3 NCOLS=1
^^WI    {
^^WI      @ 1,1 CHECKBOX MDEL 
% TRANSFER "^^WI 'Delete " // CATOM // " before run'" TO DISPLAY
% TRANSFER " 'Delete " // CATOM // " before run'" TO DISPLAY
^^WI        INFORM=NO STATE=ON
^^WI      @ 3,1 CHECKBOX MRESET 'Delete best fit data from previous runs'
^^WI        INFORM=NO
%  IF ( FILEEXISTS ( 'bestfrag.dat' ) ) THEN
^^WI        STATE=ON
%  ELSE
^^WI        STATE=OFF DISABLED=YES
%  END IF
^^WI    }
^^WI  } 
^^WI }  
^^WI SHOW 
^^CR    
%%
Waiting
%%
% LOOP 
%    VERIFY BUTTONOK BUTTONXX MPO BHELP 
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                                  
%    CASE VALUE                                                                    
%       BLOCK           %BUTTONOK%                                                  
%% delete clicked atom?
%          COPY '#EDIT 5 10'
%          CLEAR
%          INSERT 'KEEP '
%          STORE CHARACTER CATOM
%          SEND
%          COPY 'end'
^^??       MDEL STATE                                                            
%          VERIFY ON OFF                                                           
%          GET SILENT NOSTORE FINAL ABBREVIATED ' '
%          IF VALUE .EQ. 1 THEN
%            COPY '#EDIT'
%            TRANSFER 'delete ' // CATOM TO CRYSTALS
%            COPY 'end'
%          END IF
^^??       MRESET STATE                                                            
%          VERIFY ON OFF                                                           
%          GET SILENT NOSTORE FINAL ABBREVIATED ' '
%          IF VALUE .EQ. 1 THEN
%            EVALUATE LT = FILEDELETE ( 'bestfrag.dat' )
%          END IF
^^??       MCYC TEXT
%          GET SILENT NOSTORE INTEGER ' '                                             
%          EVALUATE MCYCLES = VALUE
^^??       MOCC TEXT
%          GET SILENT NOSTORE REAL ' '                                             
%          EVALUATE MOCC = VALUE
^^??       MISO TEXT
%          GET SILENT NOSTORE REAL ' '                                             
%          EVALUATE MUISO = VALUE
^^??       MJIT TEXT
%          GET SILENT NOSTORE REAL ' '                                             
%          EVALUATE MJITTER = VALUE
^^??       MFRG SELECTED  
%          GET SILENT FINAL NOSTORE INTEGER ' '                                
%          CASE VALUE
%            EVALUATE MCFILE = 'CRYSDIR:script/pf6.frg'                                          
%            EVALUATE MCFILE = 'CRYSDIR:script/bf4.frg'                                          
%            EVALUATE MCFILE = 'CRYSDIR:script/methanol.frg'                                          
%            EVALUATE MCFILE = 'CRYSDIR:script/methanol-rigid.frg'                                          
%            EVALUATE MCFILE = 'CRYSDIR:script/dcm.frg'
%            EVALUATE MCFILE = 'CRYSDIR:script/ethanol.frg'                                          
%            EVALUATE MCFILE = 'CRYSDIR:script/toluene.frg'                                          
%            EVALUATE MCFILE = 'CRYSDIR:script/benzene.frg'                                          
%            EVALUATE MCFILE = 'CRYSDIR:script/isopropanol.frg'                                          
%            EVALUATE MCFILE = 'CRYSDIR:script/acetone.frg'                                          
%            EVALUATE MCFILE = 'CRYSDIR:script/dmf.frg'                                          
%            EVALUATE MCFILE = 'CRYSDIR:script/lcamphor.frg'                                          
%          END CASE
^^??       MPO STATE                                                            
%          VERIFY ON OFF                                                           
%          GET SILENT NOSTORE FINAL ABBREVIATED ' '
%          IF VALUE .EQ. 2 THEN
^^CO          DISPOSE MASADIALOG
%             COPY '#SCRIPT MASAVOID3'                                                      
%          ELSE
^^??          MNFRAG TEXT  
%             GET SILENT FINAL NOSTORE INTEGER ' '                                
%             EVALUATE MNFRAG = VALUE
^^CO          DISPOSE MASADIALOG
%             COPY '#SCRIPT MASAPO2' 
%          END IF
%          FINISH
%       END BLOCK
%       BLOCK                   %BUTTONXX%                                          
^^WI       DISPOSE MASADIALOG
^^CR                                                                            
%          FINISH                                                                  
%       END BLOCK                                                                   
%       BLOCK                   %MPO%                                          
%          VERIFY ON OFF                                                              
%          GET SILENT NOSTORE FINAL ABBREVIATED ' ' 'ON'                              
%          CASE VALUE                                                                 
%            TRANSFER '^^CO SET MNFRAG DISABLED=NO' TO DISPLAY
%            TRANSFER '^^CO SET MNFRAG DISABLED=YES' TO DISPLAY
%            INSERT 'YES'                                                             
%          END CASE                                                                   

%       END BLOCK                                                                   
%       BLOCK                   %BHELP%                                             
%           COPY '#SCRIPT XHELP1'                                                   
Help message. TBC.
%           COPY '#SCRIPT XHELP2'                                                   
%       END BLOCK                                                                   
%    END CASE                                                                      
% END LOOP                                                                      
%END SCRIPT                                                                     
