%SCRIPT MASA
%%
%% Make up a random rotation
%%
%% Read pf6.frg line by line and compute centroid (in orth coords)
%% Re-read pf6.frg. For each line, apply random rotation, apply random shift, put atom in structure.
%%
%  VARIABLE REAL TX TY TZ
%  VARIABLE REAL XC YC ZC UISO
%  VARIABLE REAL U V W X Y Z
%  VARIABLE CHARACTER CFILE
%  VARIABLE INTEGER NTIMES
%  VARIABLE CHARACTER FELE LELE
%  VARIABLE INTEGER FSER LSER
%  VARIABLE REAL FSERR LSERR
%  VARIABLE REAL R BESTR
%  VARIABLE REAL SHIFTM OCC
%  VARIABLE INTEGER N10 T IRES MAXRES NPARTS CURRES
%  VARIABLE CHARACTER CATOM CELE
%  VARIABLE REAL RSER
%  VARIABLE CHARACTER COORD
%  VARIABLE INTEGER RESI
%%
%%
%  EVALUATE NPARTS = 2
%  EVALUATE OCC = 1.0 / REAL ( NPARTS )
%%
%  IF ( EXISTS 12 .NE. 1 ) THEN
%     COPY '#LIST 12'
%     COPY "FULL X'S"
%     COPY 'END'
%  END IF
%  COPY '#release punch tmp/masa12.dat'
%  COPY '#PUNCH 12'
%  COPY 'END'
%  COPY '#release punch logs/bfile.pch'
%%
%% If we insert molecules, they will have fragment numbers of 500+n (where n is 1,2, etc.)
%% If there are no existing fragments here, then we need to add NFRAG new ones.
%%
%% SAVE ORIGINAL INPUT DATA masa5-orig.dat (may or may not have solvent molecules added)
%%
%  COPY '#release punch tmp/masa5-orig.dat'
%  COPY '#PUNCH 5'
%  COPY 'END'
%  COPY '#release punch logs/bfile.pch'
%%
%% FIND ANY RESIDUES >= 501
%%
%  COPY '#GENERALEDIT 5'
%  COPY 'LOCATE RECORDTYPE=101'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  EVALUATE MAXRES = 500
%  LOOP
%     ON EDITERROR TERMINATE
%     COPY 'TRANSFER FROM OFFSET=16 TO IRES'
%     IF IRES .GT. MAXRES THEN
%         EVALUATE MAXRES = IRES
%     END IF
%     COPY 'NEXT'
%  END LOOP
%  COPY 'END'
%  EVALUATE MAXRES = MAXRES - 500
%%
%% If there are existing fragments in here, then don't use COORD, just keep going
%% with the first atom of each fragment being the centroid (might not be ideal for fragments with a central atom)
%%
%  IF MAXRES .GE. 1 THEN
%    IF MAXRES .NE. NPARTS THEN
 Number of overlapping mols in structure is not same as requested NPARTS
 TODO: Just remove and overwrite, for now, stop script!
%       FINISH
%    END IF
%    COPY '#SCRIPT RFACTOR'
%    EVALUATE BESTR = R
%    COPY '#release punch tmp/masa5.dat'
%    COPY '#PUNCH 5'
%    COPY 'END'
%    COPY '#release punch logs/bfile.pch'
%% Copy first atom of each residue to L10
%    COPY '#EDIT'
%    COPY 'SORT RESIDUE'
%    COPY 'END'
%%
%    QUEUE REWIND
%    QUEUE COPY #EDIT 5 10
%    COPY '#GENERALEDIT 5'
%    COPY 'LOCATE RECORDTYPE=101'
%    COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%    EVALUATE CURRES = 500
%    LOOP
%      ON EDITERROR TERMINATE
%      COPY 'TRANSFER FROM OFFSET=16 TO IRES'
%      IF IRES .GT. CURRES THEN
%         EVALUATE CURRES = IRES
%         COPY 'TRANSFER FROM OFFSET=0 TO CELE'
%         COPY 'TRANSFER FROM OFFSET=1 TO RSER'
%         TRANSFER "%QUEUE COPY KEEP " // CELE // -
'(' // CHARACTER INTEGER RSER // ')' TO SCRIPT
%      END IF
%      COPY 'NEXT'
%    END LOOP
%    COPY 'END'
%    QUEUE COPY END
%    QUEUE PROCESS
%%
%%
%%
%  ELSE
%% 
%% If no fragments present, need to add them. Use COORD.
%%
%%
%% COORD has atom to use as centroid, e.g. 'P(1)'
%%
%    EVALUATE COORD = 'B(1)'
%    EVALUATE BESTR = 200.0
%    COPY '#EDIT 5 10'
%    TRANSFER 'KEEP ' // COORD TO CRYSTALS
%    COPY 'END'
%    COPY '#EDIT'
%    TRANSFER 'DELETE ' // COORD TO CRYSTALS
%    COPY 'END'
%%
%%   Add NPARTS copies of the template
%%
%    EVALUATE CFILE = 'bf4.frg'
%    EVALUATE UISO = 0.05
%    EVALUATE SHIFTM = 0.25
%    EVALUATE RESI = 501
%    LOOP NPARTS TIMES
%       COPY '#SCRIPT MASAADD'
%       EVALUATE RESI = RESI + 1
%    END LOOP
%    COPY '#release punch tmp/masa5.dat'
%    COPY '#PUNCH 5'
%    COPY 'END'
%    COPY '#release punch logs/bfile.pch'
%  END IF
%%
%%
%%
%  COPY '#GENERALEDIT 10'
%  COPY 'TRANSHEAD FROM OFFSET=3 TO N10'
%  COPY 'END'
%  SHOW N10
%%
%%
%% INPUTS:
%% CFILE is the file to read 
%% UISO is the starting value for all the atoms in the fragment
%% SHIFTM scales the size of the random displacement from the centroid. The default is -0.5 -> +0.5 Angstroms.
%% NTIMES - number of trials
%%
%%
%  EVALUATE CFILE = 'bf4.frg'
%  EVALUATE UISO = 0.05
%  EVALUATE RESI = 501
%  EVALUATE SHIFTM = 0.25
%  EVALUATE NTIMES = 100
%%
%  LOOP NTIMES TIMES
%%
%%    Decide on one copy to remove.
%%
%     EVALUATE RESI = 501 + INTEGER ( RANDOM ( 0.0 ) * REAL NPARTS )
%     COPY '#SCRIPT MASAADD'
%% Now do some refinement. First backup the current List 12, then writes
%% a new one, then refine, then restore the original.
%%


%% Run some cycles of refinement
%     COPY '#SFLS'
%     COPY 'REFINE'
%     COPY 'REFINE'
%     COPY 'REFINE'
%     COPY 'REFINE'
%     COPY 'REFINE'
%     COPY 'END'


%%
%% Get the R-factor for this run (stored in variable R)
%%
%     COPY '#SFLS'
%     COPY 'CALC'
%     COPY 'END'
%     COPY '#SCRIPT RFACTOR'
%     IF R .LT. BESTR THEN
%       EVALUATE BESTR = R
%       COPY '#RELEASE PUNCH bestfrag.dat'
%       COPY '#PUNCH 5'
%       COPY 'END'
%       COPY '#release punch tmp/masa5.dat'
%       COPY '#PUNCH 5'
%       COPY 'END'
%       COPY '#release punch logs/bfile.pch'
%     ELSE
%% Restore last best copy
%       COPY '#USE tmp/masa5.dat'
%     END IF
%     TRANSFER ' R is ' // CHARACTER ( R ) // ', best R is ' // -
      CHARACTER ( BESTR ) TO DISPLAY
%     SHOW LOOPCOUNTER
%     COPY '#PURGE'
%     COPY 'END'
%  END LOOP
%  COPY '#USE bestfrag.dat'
%  COPY '#USE tmp/masa12.dat'
%END SCRIPT