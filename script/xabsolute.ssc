% SCRIPT XABSOLUTE
%% SEE IF THE STRUCTURE NEEDS INVERTING
%%
%   VARIABLE INTEGER FRIEDELF
%   VARIABLE LOGICAL DOFLACK          !Include Flack Parameter in LSQ
%   VARIABLE LOGICAL FDONE            !Flack parameter determined from Parsons method
%   EVALUATE FDONE = FALSE
%   
%% variables used by the question box                                           
%   VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2  
%   VARIABLE CHARACTER BUTINFO  BUTFILE
%   VARIABLE LOGICAL ANSWER 
%%
%% GET THE FRIEDELPAIRS FLAG FROM LIST 13
%   COPY '#GENERALEDIT 13'                                                      
%   COPY 'LOCATE RECORDTYPE=101'                                                
%   COPY 'TRANSFER FROM OFFSET=0 TO FRIEDELF'                                   
%   COPY 'WRITE'                                                                
%   COPY 'END'                                                                  
%  IF FRIEDELF .EQ. 0 THEN

No Friedel Pairs available.
Absolute structure not determined.


%  ELSE

Friedel Pairs are available

% copy '#set monitor fast'
%       COPY '#SCRIPT TONSWORK'
%       COPY '#SCRIPT FLACK39'
%       COPY '#TON TYPE=PASCAL PLOT=NO PUNCH=NO'
%       COPY 'END'
%  END IF
%  VARIABLE CHARACTER CFLACK
%  VARIABLE REAL SERIAL DO DSIG
%  COPY '#GENERALEDIT 39'
%  COPY 'LOCATE RECORDTYPE = 102'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  COPY 'TOP'
%  LOOP
%    ON EDITERROR TERMINATE
%    COPY 'TRANSFER FROM OFFSET=0 TO CFLACK'
%    COPY 'TRANSFER FROM OFFSET=1 TO SERIAL'
%    IF CFLACK .EQ. 'FLAC' THEN
%       IF SERIAL .EQ. 0. THEN
%        COPY 'TRANSFER FROM OFFSET=6 TO DO'
%        COPY 'TRANSFER FROM OFFSET=7 TO DSIG'
%        IF  DSIG  .GT. 0.00001 THEN
%          EVALUATE FDONE = TRUE
%        END IF
%        EVALUATE LOOPCOUNTER = LOOPEND
%       END IF
%    END IF
%    COPY 'NEXT'
%  END LOOP
%  COPY 'END'
%%
%  IF FDONE .EQ. TRUE THEN
%   COPY '#SET MONITOR ON'
%   CLEAR
%   INSERT  '{S The current value of the Flack parameter is '
%   STORE FORMAT '(F7.3)' LENGTH 8 REAL DO
%   INSERT ', '
%   STORE FORMAT '(F7.3)' LENGTH 8 REAL DSIG
%   OUTPUT
%    IF DO .GT. 0.75  THEN
%     EVALUATE QTITLE = 'Absolute Structure'                                
%     EVALUATE QLINE1 = 'Invert the absolute structure?'     
%     EVALUATE QLINE2 = 'Choose'        
%     EVALUATE BUTFILE = 'xabsolute.dat'
%     EVALUATE BUTTOK = 'Yes'                             
%     EVALUATE BUTTXX = 'No' 
%     EVALUATE BUTINFO = 'Info'                   
%     COPY '#SCRIPT XHELPQUEST'                                           
%     IF ANSWER .EQ. TRUE THEN                                          
%       COPY '#SCRIPT XINVERT'
%       COPY '#SET MONITOR FAST'     % HIDE THE SFLS ETC
%       COPY '#SCRIPT TONSWORK'
%       COPY '#SCRIPT FLACK39'
%       COPY '#SET MONITOR ON'
%       COPY '#TON TYPE=PASCAL PLOT=NO PUNCH=NO'
%       COPY 'END'
%       VARIABLE CHARACTER CFLACK
%       VARIABLE REAL SERIAL DO DSIG
%       COPY '#GENERALEDIT 39'
%       COPY 'LOCATE RECORDTYPE = 102'
%       COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%       COPY 'TOP'
%       LOOP
%        ON EDITERROR TERMINATE
%        COPY 'TRANSFER FROM OFFSET=0 TO CFLACK'
%        COPY 'TRANSFER FROM OFFSET=1 TO SERIAL'
%        IF CFLACK .EQ. 'FLAC' THEN
%         IF SERIAL .EQ. 0. THEN
%          COPY 'TRANSFER FROM OFFSET=6 TO DO'
%          COPY 'TRANSFER FROM OFFSET=7 TO DSIG'
%          IF  DSIG  .GT. 0.00001 THEN
%            EVALUATE FDONE = TRUE
%          END IF
%          EVALUATE LOOPCOUNTER = LOOPEND
%         END IF
%        END IF
%        COPY 'NEXT'
%       END LOOP
%       COPY 'END'
%       CLEAR
%       INSERT  '{S The current value of the Flack parameter is '
%       STORE FORMAT '(F7.3)' LENGTH 8 REAL DO
%       INSERT ', '
%       STORE FORMAT '(F7.3)' LENGTH 8 REAL DSIG
%       OUTPUT

{I   The structure has been inverted

%     ELSE                                                                

{I Structure not inverted
      
%     END IF  
%    ELSE IF DO .LT. 0.25 THEN

{I The structure probably does not need inverting
                                                            
%    ELSE

{I The structure may be twinned or indeterminate. 
{I You should refine the Flack Parameter


%      evaluate DOFLACK =  TRUE
%    END IF
%  END IF
% END SCRIPT
