% SCRIPT XABSOLUTE
%% Determine absolute structure.
%% The first time this is called it checks to see if the structure needs
%% inverting. If the users chooses to invert, Flack x is redetermined by
%% the Parsons method. 
%% Subsequent calld just determine the Flack parameter.
%% October 2020.  Pascals method occasionally fails (somewhere in the f90)
%% When this occurs, the script automatically tries the old DJW code
%% If this fails, nothing is done.
%% 
%   COPY '#SCRIPT FLACK39'                                                         

%%
%   VARIABLE REAL FCENTRO
%   VARIABLE INTEGER FRIEDELF  _BADPASCAL
%%   _BADPASCAL global variable set by Pascal's code when it fails
%   EVALUATE _BADPASCAL = 0
%   VARIABLE LOGICAL DOFLACK          
%% DOFLACK Include Flack Parameter in LSQ
%   VARIABLE LOGICAL FDONE            
%% FDONE Flack parameter determined from Parsons method
%   VARIABLE LOGICAL PASCALOK         
%% PASCALOK Set TRUE if Pascal's code works
%   VARIABLE INTEGER DOABS      
%% DOABS      1=Its done, 0=it's to be done
%%  DOABS values were swapped 2020 so that default L39 indicates to be done
%%
%   EVALUATE FDONE = FALSE
%   
%% variables used by the question box                                           
%   VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2  
%   VARIABLE CHARACTER BUTINFO  BUTFILE
%   VARIABLE LOGICAL ANSWER 
%%
%   IF ( EXISTS ( 13 ) .NE. 1 ) THEN                                          
%       COPY '#LIST 13'                                                         
%       COPY 'END'                                                              
%   END IF                                                                    
%%
%% Look to see if this SCRIPT has been called before
%  EVALUATE DOABS =  0
%  VARIABLE CHARACTER CABS
%  VARIABLE INTEGER ISERIAL 
%  COPY '#GENERALEDIT 39'
%  COPY 'LOCATE RECORDTYPE = 101'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  COPY 'TOP'
%  LOOP
%    ON EDITERROR TERMINATE
%    COPY 'TRANSFER FROM OFFSET=0 TO CABS'
%    COPY 'TRANSFER FROM OFFSET=1 TO ISERIAL'
%    IF CABS .EQ. 'INFO' THEN
%       IF ISERIAL .EQ. 1 THEN
%        COPY 'TRANSFER FROM OFFSET=2 TO DOABS'

%        IF  DOABS  .EQ. 1 THEN

Flack parameter has previously been determined by Parsons method 
     
%          EVALUATE FDONE = TRUE         
%        ELSE
%          EVALUATE FDONE = FALSE
%        END IF
%        EVALUATE LOOPCOUNTER = LOOPEND
%       END IF
%    END IF
%    COPY 'NEXT'
%  END LOOP
%  COPY 'WRITE OVERWRITE = OVERWRITE'
%  COPY 'END'
%%
%%
%%
%   EVALUATE FRIEDELF = - 1         
%% FRIEDELF - LIST 13 Friedel Flag                                          
%   COPY '#GENERALEDIT 2'                                                       
%   COPY 'LOCATE RECORDTYPE=101'                                                
%   COPY 'TRANSFER FROM OFFSET=0 TO FCENTRO'                                    
%   COPY 'END'                                                                  
%% GET THE FRIEDELPAIRS FLAG FROM LIST 13
%   COPY '#GENERALEDIT 13'                                                      
%   COPY 'LOCATE RECORDTYPE=101'                                                
%   COPY 'TRANSFER FROM OFFSET=0 TO FRIEDELF'                                   
%   COPY 'WRITE'                                                                
%   COPY 'END'                                                                  
% IF ( FCENTRO .GT. 0.9 ) THEN                                                  
%   EVALUATE DOFLACK = FALSE
                                                                                
{I Centro-symmetric space-group.                                                
{I Absolute structure not determined.

%   FINISH
%  ELSE IF FRIEDELF .EQ. 0 THEN
%   EVALUATE DOFLACK = FALSE

{I No Friedel Pairs available.
{I Absolute structure not determined.

%   FINISH
% ELSE

{I Friedel Pairs are available

%       copy '#set monitor fast'
%       EVALUATE DOFLACK = TRUE
%       EVALUATE PASCALOK = FALSE
%       COPY '#SCRIPT TONSWORK'
%%       COPY '#SCRIPT FLACK39'
%       COPY '#TON TYPE=PASCAL PLOT=NO PUNCH=NO'
%       COPY 'END'
%       IF _BADPASCAL .LE. 0 THEN
%          EVALUATE PASCALOK = TRUE
%       ELSE


{E Pascal's method failed
{W Trying alternative FLACK determination

%        COPY '#TON TYPE=DODC PLOT=NO PUNCH=NO'
%        COPY 'END'
%       END IF
% END IF
%%
%  VARIABLE CHARACTER CFLACK
%  VARIABLE REAL SERIAL DO DSIG
%%  Get the Flack parameter and esd to see if structure needs inverting.
%%  If the value lies between 0.25 and 0.75 suggest refinement of ENANTIO%%
%  COPY '#GENERALEDIT 39'
%  COPY 'LOCATE RECORDTYPE = 102'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  COPY 'TOP'
%  LOOP
%        ON EDITERROR TERMINATE
%        COPY 'TRANSFER FROM OFFSET=0 TO CFLACK'
%        COPY 'TRANSFER FROM OFFSET=1 TO SERIAL'
%        IF CFLACK .EQ. 'FLAC' THEN
%         IF SERIAL .EQ. 0. THEN
%          COPY 'TRANSFER FROM OFFSET=6 TO DO'
%          COPY 'TRANSFER FROM OFFSET=7 TO DSIG'
%          IF  DSIG  .GT. 0.00001 THEN
%           EVALUATE FDONE = TRUE
%          ELSE
%           EVALUATE FDONE = FALSE
%           EVALUATE DOABS = 0
%          END IF
%          EVALUATE LOOPCOUNTER = LOOPEND
%         END IF
%        END IF
%        COPY 'NEXT'
%  END LOOP
%  COPY 'LOCATE RECORDTYPE = 101'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  COPY 'TOP'
%  LOOP
%    ON EDITERROR TERMINATE
%    COPY 'TRANSFER FROM OFFSET=0 TO CABS'
%    COPY 'TRANSFER FROM OFFSET=1 TO ISERIAL'
%    IF CABS .EQ. 'INFO' THEN
%       IF ISERIAL .EQ. 1 THEN
%        IF FDONE .EQ. TRUE THEN
%         EVALUATE DOABS = 1
%         COPY 'TRANSFER TO OFFSET=2 FROM DOABS'
%         EVALUATE LOOPCOUNTER = LOOPEND
%        END IF
%       END IF
%    END IF
%    COPY 'NEXT'
%  END LOOP
%  COPY 'WRITE OVERWRITE = OVERWRITE'
%  COPY 'END'
%%
%  copy '#set monitor on'
%  IF FDONE .EQ. TRUE THEN
%   CLEAR
%   INSERT  '{S The current value of the Flack parameter is '
%   STORE FORMAT '(F7.3)' LENGTH 8 REAL DO
%   INSERT '('
%   STORE FORMAT '(F7.3)' LENGTH 8 REAL DSIG
%   INSERT ')'
%   OUTPUT
%    IF DO .GT. 0.75  THEN
%%    Value is large - structure may need inverting. If it is
%%    inverted, redo #TON to update LIST 39 with new values
%     EVALUATE QTITLE = 'Absolute Structure'                                
%     EVALUATE QLINE1 = 'Invert the absolute structure?'     
%     EVALUATE QLINE2 = 'Choose'        
%     EVALUATE BUTFILE = 'xabsolute.dat'
%     EVALUATE BUTTOK = 'Yes'                             
%     EVALUATE BUTTXX = 'No' 
%     EVALUATE BUTINFO = 'Info'                   
%     COPY '#SCRIPT XHELPQUEST'                                           
%     IF ANSWER .EQ. TRUE THEN            
{W inverting structure                              
%       COPY '#SCRIPT XINVERT'
%       COPY '#SCRIPT TONSWORK'
%%       COPY '#SCRIPT FLACK39'
%       IF PASCALOK THEN
%        COPY '#TON TYPE=PASCAL PLOT=NO PUNCH=NO'
%       ELSE
%        COPY '#TON TYPE=DODC PLOT=NO PUNCH=NO'
%       END IF
%       COPY 'END'
%       VARIABLE CHARACTER CFLACK
%       VARIABLE REAL SERIAL DO DSIG
%       COPY '#GENERALEDIT 39'
%       COPY 'LOCATE RECORDTYPE = 102'
%       COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%       COPY 'TOP'
%       LOOP
%        ON EDITERROR TERMINATE
%        COPY 'TRANSFER FROM OFFSET=0 TO CFLACK'
%        COPY 'TRANSFER FROM OFFSET=1 TO SERIAL'
%        IF CFLACK .EQ. 'FLAC' THEN
%         IF SERIAL .EQ. 0. THEN
%          COPY 'TRANSFER FROM OFFSET=6 TO DO'
%          COPY 'TRANSFER FROM OFFSET=7 TO DSIG'
%          IF  DSIG  .GT. 0.00001 THEN
%            EVALUATE FDONE = TRUE
%          END IF
%          EVALUATE LOOPCOUNTER = LOOPEND
%         END IF
%        END IF
%        COPY 'NEXT'
%       END LOOP
%       COPY 'END'
%       CLEAR
%       INSERT  '{S The new value of the Flack parameter is '
%       STORE FORMAT '(F7.3)' LENGTH 8 REAL DO
%       INSERT ', '
%       STORE FORMAT '(F7.3)' LENGTH 8 REAL DSIG
%       OUTPUT

{I   The structure has been inverted

%     ELSE                                                                

{I You chose not to invert the structure
      
%     END IF  
%    ELSE IF DO .LT. 0.25 THEN

{I The structure probably does not need inverting
                                                            
%    ELSE

{I The structure may be twinned or indeterminate. 
{I You should refine the Flack Parameter


%      EVALUATE DOFLACK =  TRUE
%    END IF
%  ELSE

{I  The absolute structure could not be determined

%  END IF
%%  COPY '#PAUSE 4'
% END SCRIPT
