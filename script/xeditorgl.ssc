%SCRIPT XEDITORGL
%%
% VARIABLE CHARACTER EDITOR_FILE EDITOR_ORIG
% VARIABLE LOGICAL EDITOR_OK
% EVALUATE EDITOR_OK = FALSE
%% The variables needed by XQUESTIO                                             
% VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX                         
% VARIABLE LOGICAL   ANSWER                                                     
%% 
%% Check it exists
%%
% IF ( .NOT. ( FILEEXISTS ( EDITOR_FILE ) ) ) THEN

 The file passed to xeditor for editing does not exist.
%    SHOW EDITOR_FILE
%    FINISH
% END IF 
%%
%%
%% Check for alternative editor user setting
%%
^^CO GETKEY CRYEDITOR
%  GET SILENT NOSTORE FINAL TEXT '1' '1'                                        
%  IF CVALUE .NE. '1' .AND. FILEEXISTS ( CVALUE ) THEN 
Using user-specified editor.
%%    TRANSFER "#SPAWN +'" // CVALUE // "' " // EDITOR_FILE TO DISPLAY
%    TRANSFER '#SPAWN +"' // CVALUE // '" ' // EDITOR_FILE TO CRYSTALS
%    EVALUATE EDITOR_OK = TRUE
%    FINISH
%  END IF
%%
%%
%TRANSFER "^^WI WINDOW XEDITOR 'Editing " // EDITOR_FILE // "'" TO DISPLAY
^^WI SIZE MODAL KEEP
^^WI DEFINEMENU MAIN 'NULL'
^^WI  MENU _FILE    '&File'
^^WI   ITEM SAVE    '&Save	Ctrl+S' 'BSS' 
^^WI   ITEM EXIT    'Save and E&xit'  'BOK'
^^WI   ITEM REVERT  '&Reload original file	Ctrl+R'  'BRR'
^^WI   ITEM CLOSE   '&Close	ALT+F4'  'BXX'
^^WI  ENDMENU
^^WI ENDDEFINEMENU
^^WI GRID OUTER NROWS=1 NCOLS=3
^^WI {
^^WI   @ 1,1 RESIZE _RSZXED VERTICAL {
^^WI     ITEM GRID _GRXEDIT NROWS=1 NCOLS=1 {
^^WI       @ 1,1 MULTIEDIT TEXT5 '' NROWS=25 NCOLS=80
^^WI         IGNORE DISABLED=NO
^^WI     }
^^WI     ITEM GRID _GREDMOD NROWS=4 NCOLS=1 {
^^WI          @ 1,1 TOOLBAR _XB                                        
^^WI               {                                                            
^^WI                 TOOL _XROT arrow.png 'Rotate structure'                           
^^WI '^^CO SET _XB ITEM _XRECT STATE OFF ITEM _XPOLY STATE OFF SET MODEL02 MODROTATE MOUSEACTION INSERTIN TEXT5'
^^WI                 TOGGLE 
^^WI                 ITEM _XROT STATE=ON                                        
^^WI                 TOOL _XRECT select.png 'Drag to select atoms'                         
^^WI '^^CO SET _XB ITEM _XROT STATE OFF ITEM _XPOLY STATE OFF SET MODEL02 MODSELECT MOUSEACTION SELECTATOM'
^^WI                 TOGGLE 
^^WI                 TOOL _XPOLY poly.png 'Draw polygon to select atoms'                        
^^WI '^^CO SET _XB ITEM _XROT STATE OFF ITEM _XRECT STATE OFF SET MODEL02 MODSELECT POLY MOUSEACTION SELECTATOM'
^^WI                 TOGGLE 
^^WI                 TOOL _UNSEL unselect.png 'Unselect all atoms'                       
^^WI '^^CO SET _XB ITEM _XROT STATE ON ITEM _XPOLY STATE OFF ITEM _XRECT STATE OFF SET MODEL02 SELECT ALL=NO MODROTATE MOUSEACTION INSERTIN TEXT5'                           
^^WI                 ENABLEIF='L1 L2 L5' DISABLEIF='SEL0'                       
^^WI                 SPLIT                                                      
^^WI                 TOOL _XZOOM tool_zoom.png 'Zoom to selected atoms'                           
^^WI '^^CO SET MODEL02 ZOOMATOMS YES SELECT ALL=NO MODROTATE MOUSEACTION INSERTIN TEXT5 SET _XB ITEM _XROT STATE ON ITEM _XPOLY STATE OFF ITEM _XRECT STATE OFF' 
^^WI                 DISABLEIF='SEL0'
^^WI                 TOOL _XZOOM tool_norm.png 'Unzoom'                         
^^WI                 '^^CO SET MODEL02 AUTOSIZE=YES ZOOMATOMS=NO'               
^^WI                 ENABLEIF='ZOOMED'                                          
^^WI                 SPLIT                                                      
^^WI                 TOOL _XBALL ball.png 'Ball and stick view'                           
^^WI '^^CO SET _XB ITEM _XBALL STATE ON ITEM _XADP STATE OFF SET MODEL02 RADTYPE=COV RADSCALE=250' 
^^WI                 TOGGLE
^^WI                 ENABLEIF='L5'
^^WI                 ITEM _XBALL STATE=ON                                       
^^WI                 TOOL _XADP elli.png 'Show displacement parameters'             
^^WI '^^CO SET _XB ITEM _XADP STATE ON ITEM _XBALL STATE OFF SET MODEL02 RADTYPE=THERMAL RADSCALE=250' 
^^WI                 TOGGLE
^^WI                   ENABLEIF='L5'
^^WI                 SPLIT                                                      
^^WI                 TOOL _XSHOWH showh.png 'Show H'           
^^WI                 'TOGGLEH' TOGGLE ENABLEIF='L5'
^^WI                 ITEM _XSHOWH STATE=ON 
^^WI                 TOOL _XCYCLER cycler.png 'Cycle residues'           
^^WI                 '^^CO SET MODEL02 CYCLER' ENABLEIF='L5'
^^WI                 DEFINEPOPUPMENU POP 'Cycle'
^^WI                 ITEM _SHOR '&Show all residues' '^^CO SET MODEL02 SHOWR'
^^WI                      ICON='nocycle.bmp' ENABLEIF='L5'
^^WI                 ITEM _ASSIGNR '&Assign residues' 'ASSIGN'
^^WI                                 ENABLEIF='L5'
^^WI                 ENDDEFINEMENU
^^WI                 TOOL _XCYCLEG cycleg.png 'Cycle group'           
^^WI                 '^^CO SET MODEL02 CYCLEG' ENABLEIF='L5'
^^WI                 DEFINEPOPUPMENU POP 'Cycle group'
^^WI                 ITEM _SHOR '&Show all disorder parts' '^^CO SET MODEL02 SHOWG'
^^WI                      ICON='nocycle.bmp' ENABLEIF='L5'
^^WI                 ENDDEFINEMENU
^^WI               }                                                            
^^WI       @ 2,1 MODELWINDOW MODEL02 'N' NROWS=5 NCOLS=65                     
^^WI            ATTACH L5 RADSCALE=250 RADTYPE=COV                              
^^WI            MOUSEACTION INSERTIN TEXT5
^^WI       @ 4,1 STATIC T1 'Click an atom to insert into editor'
^^WI     }
^^WI   }
^^WI   @ 1,3 GRID BTNS NROWS=9 NCOLS=3
^^WI   {
^^WI     @ 2,2 BUTTON BOK 'Save and e&xit' DEFAULT COMMIT
^^WI     @ 4,2 BUTTON BSS '&Save' 
^^WI     @ 6,2 BUTTON BRR '&Reload original'
^^WI     @ 8,2 BUTTON BXX '&Close' CANCEL
^^WI   }
^^WI }
^^WI SHOW
^^CR
%TRANSFER "^^CO SET TEXT5 LOAD '" // EDITOR_FILE // "'" TO DISPLAY
%%
% LOOP
%   VERIFY BOK BXX BRR BSS TOGGLEH ASSIGN
%   GET SILENT NOSTORE FINAL ABBREVIATED 'BOK?' ' '
%   IF VALUE .EQ. 1 THEN
%% save and close
%TRANSFER "^^CO SET TEXT5 EDITSAVE '" // EDITOR_FILE // "'" TO DISPLAY
^^??   _ANYTHING_ EXISTS
%      GET NOSTORE SILENT TEXT 'Waiting for save' ' '
^^CO   DISPOSE XEDITOR
%      EVALUATE EDITOR_OK = TRUE
%      FINISH
%   ELSE IF ( VALUE .EQ. 2 ) THEN
%% cancel - check for mods first
^^??   TEXT5 MODIFIED
%      VERIFY YES NO
%      GET SILENT NOSTORE FINAL ABBREVIATED 'Mod?' ' '
%      IF ( VALUE .EQ. 1 ) THEN
%         EVALUATE QTITLE = 'Unsaved changes'                                       
%         EVALUATE QLINE1 = 'Closing the editor will lose your recent changes.'              
%         EVALUATE QLINE2 = 'Are you sure?'                           
%         EVALUATE BUTTOK = 'Clo&se'                                              
%         EVALUATE BUTTXX = '&Cancel'                                               
%         COPY '#SCRIPT XQUESTIO'                                               
%         IF ANSWER .EQ. TRUE THEN                                              
^^CO         DISPOSE XEDITOR
%            FINISH
%         END IF
%      ELSE
^^CO     DISPOSE XEDITOR
%        FINISH
%      END IF
%   ELSE IF ( VALUE .EQ. 3 ) THEN
%% revert - check for mods first
^^??   TEXT5 MODIFIED
%      VERIFY YES NO
%      GET SILENT NOSTORE FINAL ABBREVIATED 'Mod?' ' '
%      IF ( VALUE .EQ. 1 ) THEN
%         EVALUATE QTITLE = 'Unsaved changes'                                       
%         EVALUATE QLINE1 = 'Reverting the text will lose your changes.'              
%         EVALUATE QLINE2 = 'Are you sure?'                           
%         EVALUATE BUTTOK = '&Revert'                                              
%         EVALUATE BUTTXX = '&Cancel'                                               
%         COPY '#SCRIPT XQUESTIO'                                               
%         IF ANSWER .EQ. TRUE THEN                                              
%TRANSFER "^^CO SET TEXT5 LOAD '" // EDITOR_ORIG // "'" TO DISPLAY
%         END IF
%      ELSE
%TRANSFER "^^CO SET TEXT5 LOAD '" // EDITOR_ORIG // "'" TO DISPLAY
%      END IF
%   ELSE IF ( VALUE .EQ. 4 ) THEN
%% Save
%TRANSFER "^^CO SET TEXT5 EDITSAVE '" // EDITOR_FILE // "'" TO DISPLAY
^^??   _ANYTHING_ EXISTS
%      GET NOSTORE SILENT TEXT 'Waiting for save' ' '
%      EVALUATE EDITOR_OK = TRUE
%% TOGGLEH
%   ELSE IF ( VALUE .EQ. 5 ) THEN
%      BLOCK                                                                        
^^?? _XB STATE _XSHOWH
%        VERIFY ON OFF
%        GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                                  
%        CASE VALUE
%          BLOCK
%% ^^CO SET _XB ITEM _XSHOWH STATE OFF
^^CO SET MODEL02 SHOWH NO
%          END BLOCK                                                                    
%          BLOCK
%% ^^CO SET _XB ITEM _XSHOWH STATE ON
^^CO SET MODEL02 SHOWH YES
%          END BLOCK                                                                    
%        END CASE                                                                      
%      END BLOCK
%% ASSIGN
%   ELSE IF ( VALUE .EQ. 6 ) THEN
%      COPY '#EDIT'
%      COPY 'INSERT RESIDUE'
%      COPY 'END'
%   END IF
% END LOOP
%END SCRIPT
