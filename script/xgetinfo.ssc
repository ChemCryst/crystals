%SCRIPT XGETINFO
%% Extensively hacked Oct 2019 to handle second INFO record, INFO(1)
%  VARIABLE LOGICAL HDONE EDONE WDONE PDONE I6DONE
%  VARIABLE INTEGER HRTYP L12SER HRESTF
%  VARIABLE REAL    TTVAL TTEMP
%%
%  VARIABLE LOGICAL FDONE  % Parsons FLACK status
%  VARIABLE INTEGER DOABS
%%
%  VARIABLE INTEGER HKEY  EKEY  WKEY  PKEY  HRKEY L12KEY I6KEY  HR2KEY
%  VARIABLE CHARACTER CKEY
%%
%% Extract relevant info from L39 about whether:
%%    H have all been found       (HDONE)
%%    Extinction has been checked (EDONE)
%%    Weights have been chosen    (WDONE)
%%    A purge of list 11 is done  (PDONE)
%%    Preferred H treatment       (HRTYP)
%%    Last list 12 written        (L12SER)
%%    Preferred H restraints      (HRESTF)
%%    Parsons FLACK status        (FDONE = T/F, DOABS = 0/1)
%%
%  VARIABLE INTEGER ITEMP
%  VARIABLE LOGICAL DOTRANSFER
%  EVALUATE DOTRANSFER = FALSE
%  VARIABLE CHARACTER CNAME
%  VARIABLE INTEGER ISERIAL
%  IF EXISTS 39 .GE. 1 THEN
%   COPY '#GENERALEDIT 39'
%   COPY 'LOCATE RECORDTYPE = 101'
%   COPY 'KEY OFFSET = 0 LENGTH = 1 MODE = CHARACTER'
%   COPY 'GROUP MODE = INTEGER'
%   COPY 'RECORD MODE = INTEGER'
%   COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%   COPY 'TOP'
%%
%   LOOP
%   EVALUATE ITEMP = LOOPCOUNTER
%    COPY 'NEXT'
%    ON EDITERROR TERMINATE
%    COPY 'TRANSFER FROM OFFSET=0 TO CNAME'
%    COPY 'TRANSFER FROM OFFSET=1 TO ISERIAL'
%    IF CNAME .EQ. 'INFO' THEN
%      IF ISERIAL .EQ. 0 THEN
%        EVALUATE DOTRANSFER = TRUE
%        EVALUATE LOOPCOUNTER = LOOPEND
%      END IF
%    END IF
%   END LOOP
%   IF DOTRANSFER THEN
%     ON EDITERROR TERMINATE
%     COPY 'TRANSFER FROM OFFSET=0 TO CKEY'
%     COPY 'TRANSFER FROM OFFSET=2 TO HKEY'
%     COPY 'TRANSFER FROM OFFSET=3 TO EKEY'
%     COPY 'TRANSFER FROM OFFSET=4 TO WKEY'
%     COPY 'TRANSFER FROM OFFSET=5 TO PKEY'
%     COPY 'TRANSFER FROM OFFSET=6 TO HRKEY'
%     COPY 'TRANSFER FROM OFFSET=7 TO L12KEY'
%     COPY 'TRANSFER FROM OFFSET=8 TO I6KEY'
%     COPY 'TRANSFER FROM OFFSET=9 TO HR2KEY'
%     IF CKEY .EQ. 'INFO' THEN
%      IF HKEY .GT. 0 THEN
%        EVALUATE HDONE = TRUE
%      END IF
%      IF EKEY .GT. 0 THEN
%        EVALUATE EDONE = TRUE
%      END IF
%      IF WKEY .GT. 0 THEN
%        EVALUATE WDONE = TRUE
%      END IF
%      IF PKEY .GT. 0 THEN
%        EVALUATE PDONE = TRUE
%      END IF
%      IF I6KEY .GT. 0 THEN
%        EVALUATE I6DONE = TRUE
%      END IF
%      EVALUATE HRTYP  = HRKEY
%      EVALUATE HRESTF  = HR2KEY
%      EVALUATE L12SER = L12KEY
%     END IF
%   END IF
%   EVALUATE DOTRANSFER = FALSE
%   COPY 'TOP'
%% Some sanity checks
%   IF HRESTF .GT. 2 then
%     EVALUATE HRESTF = 2
%   ELSE IF HRESTF .LT. 0 then
%     EVALUATE HRESTF = 0
%   END IF
%   IF HRTYP .LT. 0 then
%     EVALUATE HRTYP = 0
%   ELSE IF HRTYP .GT. 10 then
%     EVALUATE HRTYP = 10
%   END IF
%%
%   LOOP
%   EVALUATE ITEMP = LOOPCOUNTER
%    COPY 'NEXT'
%    ON EDITERROR TERMINATE
%    COPY 'TRANSFER FROM OFFSET=0 TO CNAME'
%    COPY 'TRANSFER FROM OFFSET=1 TO ISERIAL'
%    IF CNAME .EQ. 'INFO' THEN
%      IF ISERIAL .EQ. 1 THEN
%        EVALUATE DOTRANSFER = TRUE
%        EVALUATE LOOPCOUNTER = LOOPEND
%      END IF
%    END IF
%   END LOOP
%   IF DOTRANSFER THEN
%     ON EDITERROR TERMINATE
%     COPY 'TRANSFER FROM OFFSET=0 TO CKEY'
%     COPY 'TRANSFER FROM OFFSET=2 TO DOABS'
%     IF DOABS .EQ. 0 THEN
%       EVALUATE FDONE = TRUE
%     ELSE
%       EVALUATE FDONE = FALSE
%     END IF
%   END IF
%   COPY 'END'
%  END IF
%END SCRIPT
