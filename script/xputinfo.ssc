%SCRIPT XPUTINFO
%% Extensively hacked Oct 2019 to handle second info record, INFO(1)
%% Store, in LIST 39, information about whether:
%%    H have all been found       (HDONE)
%%    Extinction has been checked (EDONE)
%%    Weights have been chosen    (WDONE)
%%    Purge of 11 done            (PDONE)
%%
%% NOTE FDONE [INFO(1)] is set in XABSOLUTE
%
%%    Twintolerance value last used (TTVAL)
%%
%  VARIABLE LOGICAL HMASK EMASK WMASK PMASK I6MASK
%  VARIABLE LOGICAL HDONE EDONE WDONE PDONE I6DONE
%%
%  VARIABLE LOGICAL HRMASK L12SMASK HRESTM
%  VARIABLE INTEGER HRTYP  L12SER   HRESTF
%%
%  VARIABLE LOGICAL TTMASK
%  VARIABLE REAL    TTVAL
%%
%  IF EXISTS 39 .LT. 1 THEN
%      COPY '#LIST 39'
%      COPY 'READ NINT=1 '
%      COPY 'INTEGER INFO 0'
%      COPY 'END'
%  END IF
%  VARIABLE INTEGER ITEMP
%  VARIABLE LOGICAL DOINSERT
%  VARIABLE CHARACTER CNAME
%  VARIABLE INTEGER ISERIAL
%  EVALUATE DOINSERT = TRUE
%  COPY '#GENERALEDIT 39'
%  COPY 'LOCATE RECORDTYPE = 101'
%  COPY 'KEY OFFSET = 0 LENGTH = 1 MODE = CHARACTER'
%  COPY 'GROUP MODE = INTEGER'
%  COPY 'RECORD MODE = INTEGER'
%  COPY 'ERROR MESS=YES SIGN=WARN ACTI=CONT NAME=EDITERROR'
%  COPY 'TOP'
%%
%  LOOP
%    EVALUATE ITEMP = LOOPCOUNTER
%    COPY 'NEXT'
%    ON EDITERROR TERMINATE
%    COPY 'TRANSFER FROM OFFSET=0 TO CNAME'
%    COPY 'TRANSFER FROM OFFSET=1 TO ISERIAL'
%    IF CNAME .EQ. 'INFO' THEN
%      IF ISERIAL .EQ. 0 THEN
%        EVALUATE DOINSERT = FALSE
%        EVALUATE LOOPCOUNTER = LOOPEND
%      END IF
%    END IF
%  END LOOP
%%
%%
%  IF DOINSERT THEN
%     COPY 'INSERT'
%  END IF
%  COPY 'CHANGE 0 CHAR CHAR = INFO'
%  COPY 'CHANGE 1 INTEGER INTEGER = 0'
%  IF ( HMASK .EQ. TRUE ) THEN
%    IF ( HDONE .EQ. TRUE ) THEN
%      COPY 'CHANGE 2 INTEGER INTEGER = 1'
%    ELSE
%      COPY 'CHANGE 2 INTEGER INTEGER = 0'
%    END IF
%  END IF
%  IF ( EMASK .EQ. TRUE ) THEN
%    IF ( EDONE .EQ. TRUE ) THEN
%      COPY 'CHANGE 3 INTEGER INTEGER = 1'
%    ELSE
%      COPY 'CHANGE 3 INTEGER INTEGER = 0'
%    END IF
%  END IF
%  IF ( WMASK .EQ. TRUE ) THEN
%    IF ( WDONE .EQ. TRUE ) THEN
%      COPY 'CHANGE 4 INTEGER INTEGER = 1'
%    ELSE
%      COPY 'CHANGE 4 INTEGER INTEGER = 0'
%    END IF
%  END IF
%  IF ( PMASK .EQ. TRUE ) THEN
%    IF ( PDONE .EQ. TRUE ) THEN
%      COPY 'CHANGE 5 INTEGER INTEGER = 1'
%    ELSE
%      COPY 'CHANGE 5 INTEGER INTEGER = 0'
%    END IF
%  END IF
%  IF ( HRMASK ) THEN
%    IF ABS ( REAL HRTYP ) .GT. 9999.0 THEN
{E HRTYP preference value invalid. Reset to zero.
%      EVALUATE HRTYP = 0
%    END IF
%    CLEAR
%    INSERT 'CHANGE 6 INTEGER INTEGER = '
%    STORE FORMAT /(I4)/ LENGTH 4 INTEGER HRTYP
%    SEND
%  END IF
%  IF ( L12SMASK ) THEN
%    CLEAR
%    INSERT 'CHANGE 7 INTEGER INTEGER = '
%    STORE FORMAT /(I6)/ LENGTH 6 INTEGER L12SER
%    SEND
%  END IF
%  IF ( I6MASK .EQ. TRUE ) THEN
%    IF ( I6DONE .EQ. TRUE ) THEN
%      COPY 'CHANGE 8 INTEGER INTEGER = 1'
%    ELSE
%      COPY 'CHANGE 8 INTEGER INTEGER = 0'
%    END IF
%  END IF
%  IF ( HRESTM ) THEN
%    IF ABS ( REAL HRESTF ) .GT. 9999.0 THEN
{E HRTYP preference value invalid. Reset to zero.
%      EVALUATE HRESTF = 0
%    END IF
%    CLEAR
%    INSERT 'CHANGE 9 INTEGER INTEGER = '
%    STORE FORMAT /(I4)/ LENGTH 4 INTEGER HRESTF
%    SEND
%  END IF
%%
%% TTVAL - Twin tolerance.
%  COPY 'WRITE OVERWRITE=OVERWRITE'
%  COPY 'LOCATE RECORDTYPE = 102'
%  COPY 'KEY OFFSET = 0 LENGTH = 1 MODE = CHARACTER'
%  COPY 'GROUP MODE = REAL'
%  COPY 'RECORD MODE = REAL'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%%
%  EVALUATE DOINSERT = TRUE
%  BLOCK
%    ON EDITERROR TERMINATE
%    COPY 'CHECK'
%    COPY 'SEARCH CHAR = INFO'
%    EVALUATE DOINSERT = FALSE
%  END BLOCK
%  IF DOINSERT THEN
%     COPY 'INSERT'
%  END IF
%  COPY 'CHANGE 0 CHAR CHAR = INFO'
%  COPY 'CHANGE 1 REAL REAL = 0.0'
%  IF ( TTMASK .EQ. TRUE ) THEN
%    CLEAR
%    INSERT 'CHANGE 1 REAL REAL = '
%    STORE FORMAT /(F10.6)/ LENGTH 10 REAL TTVAL
%    SEND
%  END IF
%  COPY 'END'
%%{R * Guide information stored (in list 39).
%END SCRIPT
