%SCRIPT XSTICKB
%% FIX all atoms except the selected ones
%%
%% All values have 128 (bit7) set to indicate sticky property.
%% 0 (128) = no stick - 0 or 128 are equivalent
%% 1 (129) = x's 
%% 2 (130) = x's,uiso
%% 3 (131) = x's,u'scale
%% 4 (132) = fix 
%% 10 (138) = ride
%%
%%
%  VARIABLE CHARACTER CELE CATM CCHK
%  VARIABLE REAL RCHK RSER 
%  VARIABLE INTEGER NINDEX ISER IREF REFMODE
%  VARIABLE LOGICAL CALLED
%%
%% Get atom name
%%
%  IF CALLED .EQ. FALSE THEN
%%   First set all atoms to FIXED
%    COPY '#EDIT'
%    COPY 'CHANGE FIRST(REF) UNTIL LAST 132'
%    COPY 'END'
%%
%    GET SILENT NOSTORE FINAL KEYWORD 'Atom?' 'C(1)'
%    EVALUATE CATM = CVALUE
%    VERIFY XS ISO ANISO DEFAULT
%    GET SILENT NOSTORE FINAL ABBREVIATED 'Refmode?' 'XS'
%    EVALUATE REFMODE = VALUE
%    CASE REFMODE
%      EVALUATE IREF = 129
%      EVALUATE IREF = 130
%      EVALUATE IREF = 131
%      EVALUATE IREF = 0
%    END CASE
%%
%    IF IREF .EQ. 0 THEN
%%    UN-FIX all the atoms
%     COPY '#EDIT'
%     COPY 'CHANGE FIRST(REF) UNTIL LAST 0'
%     COPY 'END'
%     FINISH
%    END IF
%%
%  END IF
%  EVALUATE CELE = FIRSTSTR ( CATM )
%  EVALUATE ISER = FIRSTINT ( CATM )
%%
%%
%%
%  BLOCK
%    EVALUATE NINDEX = 0
%    ON EDITERROR TERMINATE
%    IF ( CALLED .EQ. FALSE ) THEN
%       COPY '#GENERALEDIT 5'
%       COPY 'LOCATE RECORDTYPE=101'
%       COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%    END IF
%    LOOP 
%     ON EDITERROR TERMINATE
%     COPY 'TRANSFER FROM OFFSET=0 TO CCHK'
%     COPY 'TRANSFER FROM OFFSET=1 TO RCHK'
%     IF ( ( CELE .EQ. CCHK ) .AND. ( ISER .EQ. INTEGER ( RCHK ) ) ) THEN
%      COPY 'TRANSFER TO OFFSET=15 FROM IREF'
%      IF ( CALLED .EQ. FALSE ) THEN
%        COPY 'WRITE'
%        COPY 'END'
%      END IF
%      EVALUATE LOOPEND = LOOPCOUNTER
%      EVALUATE NINDEX = LOOPCOUNTER
%     END IF
%     COPY 'NEXT'
%    END LOOP
%  END BLOCK
%  IF ( NINDEX .EQ. 0 ) THEN
%   TRANSFER '{E Atom not found. ' // CATM TO DISPLAY
%   FINISH
%  END IF
%%
%END SCRIPT
