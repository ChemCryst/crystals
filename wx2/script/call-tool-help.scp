%SCRIPT call-tool-help
%%
%% TOOL-HELP is set to TRUE by TEST-TOOL-HELP if the global key is set
%% TOOL-TEXT is the name of the operation 
%% TOOL-FILE is the help-file built by adding ".html" to the operation name
%% _____________________________________________________________________
%  VARIABLE LOGICAL TOOL-HELP
%  COPY '#SCRIPT TEST-TOOL-HELP'
%%
%  VARIABLE CHARACTER TOOL-FOLDER
%  VARIABLE CHARACTER TOOL-TEXT TOOL-FILE
%  VARIABLE CHARACTER FULL-FILE
%  EVALUATE FULL-FILE = '#SPAWN "CRYSDIR:manual/' // TOOL-FOLDER // '/'  // TOOL-FILE // '.html"'
%  VARIABLE CHARACTER BUTTOK BUTTXX 
%  VARIABLE LOGICAL RUNNOW
%  VARIABLE INTEGER JVALUE
%  EVALUATE RUNNOW = TRUE  
% IF TOOL-HELP .EQ. TRUE THEN
^^WI WINDOW TOOLHELPWINDOW 
% TRANSFER '^^WI "' // TOOL-TEXT // '" ' TO DISPLAY
^^WI MODAL COMMIT='CFBOK' CANCEL='CFBXX'
^^WI GRID GRIDM NROWS=11 NCOLS=3  
^^WI { 
^^WI   @ 2,2 GRID SH NROWS=1 NCOLS=3  
^^WI   {  
%   TRANSFER "^^WI @ 1,1 STATIC T1 '" // TOOL-TEXT // "'" TO DISPLAY
^^WI    @ 1,2 STRETCH SHH HORIZONTAL                                            
^^WI    @ 1,3 BUTTON CFHELP '&Show help' 
^^WI   }  
^^WI   @ 4,2 STATIC TT 
^^WI 'Tool help is switched on. This feature allows you to find out what the'
^^WI   @ 5,2 STATIC TU 
^^WI 'program is about to do before continuing.'
^^WI   @ 7,2 STATIC TV 
^^WI ' Uncheck the box below if you do not want to see these messages.'
^^WI   @ 8,2 CHECKBOX SHOWTOOL 'Show tool help' STATE=ON                 
^^WI   @ 10,2 GRID GB NROWS=1 NCOLS=5  
^^WI   {  
^^WI    @ 1,1 BUTTON CFBOK '&Continue' DEFAULT  
^^WI    @ 1,3 BUTTON CFBXX 'Cl&ose' 
^^WI   }  
^^WI }  
^^WI SHOW  
^^CR  
%  EVALUATE RUNNOW = TRUE  
%   LOOP 
%     VERIFY  CFBXX CFHELP CFBOK  
%     GET SILENT NOSTORE FINAL ABBREVIATED 'BXX?' 'BXX'
%     CASE VALUE  
%      BLOCK %CFBXX% 
%% TRANSFER '{I "' // TOOL-TEXT // '" cancelled' to display
%        EVALUATE RUNNOW = FALSE
%          EVALUATE LOOPEND = LOOPCOUNTER  
%      END BLOCK 
%% 
%      BLOCK %CFHELP%
%      TRANSFER  FULL-FILE TO CRYSTALS
%      END BLOCK
%%
%      BLOCK %CFBOK%  
%          EVALUATE RUNNOW = TRUE 
%          EVALUATE LOOPEND = LOOPCOUNTER  
%      END BLOCK  
%    END CASE  
%  END LOOP  
%%
%% Only get here if Close or Continue pressed.
%%
^^?? SHOWTOOL STATE
%  VERIFY ON OFF
%  GET SILENT NOSTORE FINAL ABBREVIATED ' '                                     
%  EVALUATE JVALUE = VALUE
%  IF JVALUE .EQ. 1 THEN  
{I Tool help ON
^^CO  SETKEY TOOL-HELP 0               
%  ELSE                                                                      
{I Tool help OFF
^^CO  SETKEY TOOL-HELP 1               
%  END IF                                                                    
^^CO       DISPOSE TOOLHELPWINDOW  
%%  
% END IF
%% _____________________________________________________________________
%%
%END SCRIPT



