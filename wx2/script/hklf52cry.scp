%SCRIPT HKLF52CRY   
%%  
%% Run hklf52cry with common options.  
%%  
%  VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2
%  VARIABLE LOGICAL ANSWER
%% C_FILE MAY BE PASSED IN BY XFILE_ANY
%  vARIABLE CHARACTER C_FILE CFILE
%  EVALUATE CFILE = 'NONE'
%  VARIABLE CHARACTER CIN COUT CARG  
%  VARIABLE LOGICAL NEW25 RUNNOW
%  VARIABLE REAL VOLF  
%  VARIABLE INTEGER N25 NEL LOOPCOUNTER 
%  VARIABLE LOGICAL EDITOR_OK
%  VARIABLE CHARACTER EDITOR_FILE READER_FILE
%  EVALUATE N25 = 0
%  EVALUATE RUNNOW = FALSE  
%  EVALUATE NEW25 = FALSE  
%  VARIABLE LOGICAL USE6
%  EVALUATE USE6 = FALSE
%  VARIABLE LOGICAL USE25
%  EVALUATE USE25 = FALSE
%%
%%
%% IF FILENAME PASSED IN, JUST DO IT OTHERWISE OPEN WINDOW
%  IF .NOT. FILEEXISTS ( C_FILE )  THEN
%%
^^WI WINDOW HKLF52CRY 'hklf5cry' MODAL COMMIT='CFBOK' CANCEL='CFBXX'  
^^WI GRID GRIDM NROWS=6 NCOLS=3  
^^WI {  
^^WI @ 2,2 GRID CFG NROWS=1 NCOLS=3 OUTLINE='hklf5 to convert:'  
^^WI {  
^^WI @ 1,1 EDITBOX CINILE ' ' CHARS=48 INFORM=NO INPUT  
^^WI @ 1,3 BUTTON BROWSE 'Browse...'  
^^WI }  
^^WI @ 4,2 GRID GB NROWS=3 NCOLS=5  
^^WI {  
^^WI @ 1,1 STATIC T1 'Output file:'  
^^WI @ 1,3 EDITBOX OFNAM 'hklf52cry.cry'  
^^WI }  
^^WI @ 5,2 GRID GB NROWS=1 NCOLS=5  
^^WI {  
^^WI @ 1,1 BUTTON CFBOK '&Ok' DEFAULT  
^^WI @ 1,3 BUTTON CFBXX '&Cancel' 
^^WI @ 1,5 BUTTON CFHELP 'Help' 
^^WI }  
^^WI }  
^^WI SHOW  
^^CR  
%  LOOP  
%    VERIFY BROWSE CFBXX CFBOK  CFHELP
%    GET SILENT NOSTORE FINAL ABBREVIATED 'BXX?' 'BXX'
%    CASE VALUE  
%      BLOCK %BROWSE%  
^^WI SYSOPENFILE '*.hkl;*.hklf5;*.dat;' 'HKL format (*.hkl*,*.dat)'  
^^CR  
%        GET SILENT TEXT 'New filename:'  
%        IF CVALUE .NE. 'CANCEL' THEN  
%          CLEAR  
^^WI SET CINILE TEXT  
%          TRANSFER "^^WI '" // CVALUE // "'" TO DISPLAY  
^^CR  
%          EVALUATE CIN = CVALUE  
%          IF ( .NOT. ( FILEEXISTS CIN ) ) THEN  
{E Error: Filename given does not exist.  
%          END IF  
%        END IF  
%      END BLOCK  
%      BLOCK %CFBXX%  
^^CO DISPOSE HKLF52CRY  
%        FINISH  
%      END BLOCK  
%      BLOCK %CFBOK%  
^^?? CINILE TEXT  
%        GET SILENT TEXT 'CIN name '  
%        EVALUATE CIN = CVALUE  
%        IF ( .NOT. ( FILEEXISTS CIN ) ) THEN  
{E Error: Filename given does not exist.  
%          ELSE  
^^?? OFNAM TEXT  
%          GET SILENT TEXT 'Output file '  
%          EVALUATE COUT = CVALUE  
%%  
^^CO DISPOSE HKLF52CRY  
%          EVALUATE LOOPEND = LOOPCOUNTER  
%        END IF  
%      END BLOCK  
%      BLOCK %CFHELP%
%       COPY '#SPAWN "CRYSDIR:manual/Xray-data/table.html"'
%      END BLOCK
%    END CASE  
%  END LOOP  
%%  
%  ELSE
%%   FILENAME PASSED IN - COPY TO LOCAL VARIABLES
%    EVALUATE CIN = C_FILE
%    EVALUATE COUT = 'hklf52cry.cry'
%  END IF
%
%% Launch HKLF5 TO CRY  
%%  
%% Remove any previous files
%  VARIABLE LOGICAL T
%  EVALUATE T = FILEDELETE ( 'l25.cry' )
%  EVALUATE T = FILEDELETE ( cout )
%%
%  variable character ccommand  
%  evaluate ccommand = '#SPAWN + "CRYSDIR:hklf52cry.exe" "' -  
// cin // '" -o ' // cout  
%  TRANSFER ccommand TO display  
%  TRANSFER ccommand TO CRYSTALS  
%%  
%%  
%%  START PROCESSING THE OUTPUT
%%  
%  if ( fileexists 'l25.cry' ) then
%    EVALUATE RUNNOW = TRUE
%%  Show the matrix


{I CRYSTALS thinks that the twin laws are:
%    copy '#type l25.cry'
%  else
%    EVALUATE RUNNOW = FALSE

{E  CRYSTALS is having problems understanding the hkl5 file
{E  and cannot find a convincing twin law.
{E
{E  The most common reason is that the HKLF5 file
{E  contains merged reflections.
{E
{E  Create an unmerged file which will be merged in CRYSTALS.
{E  Look in TLAW_OUT.LIS for details.

%  end if
%%  
^^WI WINDOW A1 'Process Twin output' SIZE KEEP MODAL
^^WI CANCEL='RF_BXX'
^^WI GRID MAIN NROWS=3 NCOLS=2
^^WI {
^^WI   @ 1,1 GRID TEXT NROWS=7  NCOLS=3 OUTLINE = ' '
^^WI   {
^^WI    @ 1,1 STATIC TEXT1
^^WI    'Look in the GUI text output for the sugggested twin laws'
^^WI    @ 3,1 STATIC TEXT1 
^^WI    'If CRYSTALS has found the correct twin law, choose Input Twin Laws'
^^WI    @ 4,1 STATIC TEXT1 
^^WI    'If you have other ideas, you can edit the Twin Laws File'
^^WI    @ 5,1 STATIC TEXT1 
^^WI    'If you know the Twin Laws from the instrument, Manually Input them'
^^WI    @ 6,1 STATIC TEXT1 
^^WI    'If you need more facts. look at the log file'
^^WI    @ 7,1 STATIC TEXT1 
^^WI    'If you already have a LIST 25, input the reflections anyway'
^^WI   }
^^WI   @ 3,1 GRID BTNS NROWS=4 NCOLS=3 OUTLINE=' '
^^WI   {
^^WI       @ 1,1 RADIOBUTTON ITYP11 'Input Twin Laws'    STATE=OFF INFORM
^^WI       @ 1,2 RADIOBUTTON IPUN12 'Look at LOG file'     STATE=OFF INFORM
^^WI       @ 1,3 RADIOBUTTON IPUN13 'Input reflections anyway'     STATE=OFF INFORM
^^WI       @ 2,1 RADIOBUTTON ITYP21 'Edit Matrix File'     STATE=OFF INFORM
^^WI       @ 2,2 RADIOBUTTON ITYP22 'Manual Matrix Input'   STATE=OFF INFORM
^^WI       @ 2,3 RADIOBUTTON ITYP23 'LIST 25 file' STATE=OFF INFORM
^^WI       @ 4,1 BUTTON RF_BXX 'Quit' 
^^WI       @ 4,2 BUTTON RF_CONT 'Continue' 
^^WI       @ 4,3 BUTTON RF_HELP 'Help'        
^^WI   }     
%      if RUNNOW .eq. false then
^^WI   @ 2,1 GRID TEXT NROWS=3 NCOLS=3 OUTLINE=' '
^^WI   {
^^WI     @ 1,2 STATIC TEXT1
^^WI     'CRYSTALS failed to find a matrix.'
^^WI     @ 2,2 STATIC TEXT2
^^WI     'You should probably abort processing the reflections'
^^WI     @ 3,2 STATIC TEXT3
^^WI     'unless you have already input valid twin matrices'
^^WI   }
%      end if
^^WI }
^^WI SHOW
^^CR
% VARIABLE INTEGER JLOOP IDJW 
%%
% LOOP
%  EVALUATE JLOOP = LOOPCOUNTER
%  VERIFY  RF_BXX ITYP11 IPUN12 IPUN13 ITYP21 ITYP22 ITYP23 RF_HELP RF_CONT
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  EVALUATE IDJW = VALUE
%  CASE VALUE
%   BLOCK                                           %RF_BXX - QUIT%
%       EVALUATE LOOPCOUNTER = LOOPEND
%   END BLOCK
%%
%   BLOCK                                           %READ MATRIX%
%    IF FILEEXISTS ( 'l25.cry' ) THEN
%     COPY '#USE l25.cry'
%     EVALUATE RUNNOW = TRUE
%     EVALUATE NEW25 = TRUE
%     EVALUATE USE6 = TRUE
%     EVALUATE LOOPCOUNTER = LOOPEND
%    ELSE
{E  A matrix has not been computed
%    END IF   
%   END BLOCK 
%%
%   BLOCK                                           %LOOK AT LOG%
%    IF FILEEXISTS ( 'TLAW_OUT.LIS' ) THEN
%      EVALUATE READER_FILE = 'TLAW_OUT.LIS'
%      COPY '#SCRIPT XREADER'
%    ELSE
{E  CRYSTALS failed to process the HKLF 5 file
%    END IF
%   END BLOCK
%%
%   BLOCK                                         %IMPORT REFLECTIONS ANYWAY
{I Try to read the hklf52cry.cry file
{I You should look in the log file to see what happened
{I during the processing of the hklf5 file

%    IF FILEEXISTS ( 'TLAW_OUT.LIS' ) THEN
%      EVALUATE READER_FILE = 'TLAW_OUT.LIS'
%      COPY '#SCRIPT XREADER'
%    ELSE
{E  CRYSTALS failed to find the twin laws
%    END IF
%    IF FILEEXISTS ( 'hklf52cry.cry' ) THEN
%       EVALUATE RUNNOW = TRUE
%%       EVALUATE NEW25 = TRUE
%       EVALUATE USE6 = TRUE
%       EVALUATE LOOPCOUNTER = LOOPEND
%    ELSE
{E  File hklf52cry.cry does not exist
%        EVALUATE USE6 = FALSE
%    END IF
%   END BLOCK
%%
%   BLOCK                                           %EDIT MATRIX%
%    IF FILEEXISTS ( 'l25.cry' ) THEN
%      EVALUATE EDITOR_FILE = 'l25.cry'
%      COPY '#SCRIPT XEDITOR'
%      IF ( .NOT. EDITOR_OK ) THEN
 Editing abandoned
%      ELSE
%        EVALUATE USE6 = TRUE
%        EVALUATE LOOPCOUNTER = LOOPEND
%      END IF                                                                             
%    ELSE
{E  There is no matrix file (l25.cry) to edit
%    END IF   
%   END BLOCK
%%
%   BLOCK                                           %MANUAL MATRIX%
%    copy '#script IN25'
%    IF ( EXISTS 25 .EQ. 1 ) THEN
%     EVALUATE RUNNOW = TRUE
%     EVALUATE NEW25 = TRUE
%     EVALUATE USE6 = TRUE
%    END IF
%    EVALUATE LOOPCOUNTER = LOOPEND
%   END BLOCK
%%
%   BLOCK                                           %INPUT LIST 25%
%    copy '#SCRIPT BROWSE'
%    IF RUNNOW THEN
%        TRANSFER '#USE "' // CFILE // '"' TO CRYSTALS
%    ELSE
 Cannot find a LIST 25 file
%     EVALUATE LOOPCOUNTER = LOOPEND
%    END IF
%    IF ( EXISTS 25 .EQ. 1 ) THEN
%     EVALUATE RUNNOW = TRUE
%     EVALUATE USE25 = TRUE
%     EVALUATE USE6 = TRUE
%    END IF
%    EVALUATE LOOPCOUNTER = LOOPEND
%   END BLOCK
%%
%   BLOCK                                           %HELP%
%    COPY '#SPAWN "CRYSDIR:manual/Xray-data/hklf52cry.html"'
%   END BLOCK
%%
%   BLOCK                                           %CONTINUE%
{I   Choose an option
%   END BLOCK
%%
%  END CASE
% END LOOP
^^CO    DISPOSE A1
%%
%  IF USE6 .EQ. FALSE THEN
{E The reflections will not be processed
%   FINISH
%  END IF

{I The reflections will be processed after some
{I intenal checks have been made


%% MODIFY LISTS 5, 13 AND 25
%  IF ( EXISTS 5 .NE. 1 ) THEN  
%   COPY '#LIST 5'
%   COPY 'READ NATOM=0'
%   COPY 'END'
%  END IF
%%
%  IF ( NEW25 ) THEN
%   IF ( EXISTS 25 .EQ. 1 ) THEN  
Found List 25 (twin laws), enabling twin flag  
%    VARIABLE INTEGER ITMP  
%    COPY '#GENERALEDIT 13'  
%    COPY 'LOCATE RECORDTYPE=101'  
%% Check for twin laws. If present, restore L13 flag.  
%    EVALUATE ITMP = 0  
%    COPY 'TRANSFER TO OFFSET=1 FROM ITMP'  
%    COPY 'WRITE'  
%    COPY 'END'  
%%  
%    EVALUATE VOLF = 1.  
%    COPY '#GENERALEDIT 25'  
%    COPY 'TRANSHEAD FROM OFFSET=3 TO N25'  
%    COPY 'GETSERIAL ORIGSER'  
%    COPY 'END'  
%    EVALUATE VOLF = 1. / REAL N25  
%   END IF  
%%  
%   IF N25 .GE. 2 THEN  
Resetting element scale factors  
%      COPY '#GENERALEDIT 5'  
%      COPY 'TRANSHEAD FROM OFFSET=22 TO NEL'  
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'  
%      COPY 'LOCATE RECORDTYPE=105'  
%% Insert new record.  
%      COPY 'TRANSHEAD TO OFFSET=22 FROM N25'  
%      COPY 'INSERT'  
%      IF NEL .GT. 0 THEN  
%% Remove old record  
%        COPY 'DELETE'  
%      END IF  
%      COPY 'TRANSHEAD TO OFFSET=22 FROM N25'  
%      COPY 'TOP' 
%      LOOP N25 TIMES
%        CASE LOOPCOUNTER
%          COPY 'TRANSFER TO OFFSET = 0 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 1 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 2 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 3 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 4 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 5 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 6 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 7 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 8 FROM VOLF'  
%        END CASE  
%      END LOOP  
%      COPY 'WRITE'  
%      COPY 'END'  
%   END IF  
%  END IF  
%%  
%%  
%% Only import reflections if LIST 25 exists
%  IF ( .NOT. RUNNOW ) THEN

{E  A matrix could not be extracted from the HKLF5 file.
{E  The most common reason is that the HKLF5 file
{E  contains merged reflections.
{E  Create an unmerged file which will be merged in CRYSTALS.
{E  Look in TLAW_OUT.LIS for details
%  ELSE
%%  
%   IF ( ( EXISTS 25 ) .GE. 1 ) THEN  
%     EVALUATE QTITLE = 'Import reflections'
%     EVALUATE QLINE1 = 'Do you want to import the reflections?'
%     EVALUATE QLINE2 = ' '
%     EVALUATE BUTTOK = '&Yes'
%     EVALUATE BUTTXX = '&No'
%     COPY '#SCRIPT XQUESTIO'
%     IF ANSWER .EQ. FALSE THEN
%       FINISH
%     ELSE
%       TRANSFER '#USE ' // COUT TO CRYSTALS  
%         VARIABLE LOGICAL XQMERGE
%% Ask user if they want to merge reflections:                                  
%         COPY '#SCRIPT XFRIEDEL'
%         MESSAGE USER1 ' Error during reflection processing'                   
%         ON ERROR TERMINATE USER1                                              
{S * Removing systematically absent reflections                                 
%         COPY '#SYSTEMATIC'                                                    
%         COPY 'END'                                                            
%         CLEAR                                                                 
{S * Sorting reflections                                                        
%         COPY '#SORT'                                                          
%         COPY 'END'                                                            
%         CLEAR                                                                 
%         IF ( XQMERGE ) THEN                                                   
{S * Merging equivalent reflections                                             
%           CLEAR                                                               
%           COPY '#MERGE'   
%           COPY 'REFLECTIONS LIST=HI'
%           COPY 'REJECT SIGMA=2'
%           COPY 'END'                                                          
%         END IF                                                                
%%                                                                               
%     END IF
%   ELSE
{E  CRYSTALS must have twin laws (LIST 25) before twinned
{E  reflections can be processed
%   END IF
%  END IF
%%  
% END SCRIPT  
